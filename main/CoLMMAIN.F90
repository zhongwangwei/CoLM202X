#include <define.h>

SUBROUTINE CoLMMAIN ( &

         ! model running information
           ipatch,       idate,        coszen,       deltim,       &
           patchlonr,    patchlatr,    patchclass,   patchtype,    &
           doalb,        dolai,        dosst,        oro,          &

         ! soil information and lake depth
           soil_s_v_alb, soil_d_v_alb, soil_s_n_alb, soil_d_n_alb, &
           vf_quartz,    vf_gravels,   vf_om,        vf_sand,      &
           wf_gravels,   wf_sand,      porsl,        psi0,         &
           bsw,          theta_r,      fsatmax,      fsatdcf,      &
           topoweti,     alp_twi,      chi_twi,      mu_twi,       &
#ifdef vanGenuchten_Mualem_SOIL_MODEL
           alpha_vgm,    n_vgm,        L_vgm,        &
           sc_vgm,       fc_vgm,       &
#endif
           hksati,       csol,         k_solids,     dksatu,       &
           dksatf,       dkdry,        BA_alpha,     BA_beta,      &
           rootfr,       lakedepth,    dz_lake,      elvstd,  BVIC,&
#if (defined CaMa_Flood)
           ! add flood depth, flood fraction, flood evaporation and
           ! flood re-infiltration
           flddepth,     fldfrc,       fevpg_fld,    qinfl_fld,    &
#endif

         ! vegetation information
           htop,         hbot,         sqrtdi,       &
           effcon,       vmax25,       c3c4,                       &
           kmax_sun,     kmax_sha,     kmax_xyl,     kmax_root,    &
           psi50_sun,    psi50_sha,    psi50_xyl,    psi50_root,   &
           ck,           slti,         hlti,         shti,         &
           hhti,         trda,         trdm,         trop,         &
           g1,           g0,           gradm,        binter,       &
           extkn,        chil,         rho,          tau,          &
#ifdef HYPERSPECTRAL
           ! variables for hyperspectral scheme
           clr_frac,    cld_frac,                                   &
           reflectance, transmittance,                              &
           soil_alb,    kw, nw,                                     &
#endif

         ! atmospheric forcing
           forc_pco2m,   forc_po2m,    forc_us,      forc_vs,      &
           forc_t,       forc_q,       forc_prc,     forc_prl,     &
           forc_rain,    forc_snow,    forc_psrf,    forc_pbot,    &
           forc_sols,    forc_soll,    forc_solsd,   forc_solld,   &
           forc_frl,     forc_hgt_u,   forc_hgt_t,   forc_hgt_q,   &
           forc_rhoair,  &
#ifdef HYPERSPECTRAL
           forc_solarin,                                         &
#endif

           ! cbl forcing
           forc_hpbl,    &
           ! aerosol deposition
           forc_aerdep,  &

         ! land surface variables required for restart
           z_sno,        dz_sno,       t_soisno,     wliq_soisno,  &
           wice_soisno,  smp,          hk,           t_grnd,       &
           tleaf,        ldew,         ldew_rain,    ldew_snow,    &
           fwet_snow,    sag,          scv,          snowdp,       &
           fveg,         fsno,         sigf,         green,        &
           lai,          sai,          alb,          ssun,         &
           ssha,         ssoi,         ssno,         thermk,       &
           extkb,        extkd,        vegwp,        gs0sun,       &
           gs0sha,       &
#ifdef HYPERSPECTRAL
           alb_hires,    &
           sol_dir_ln_hires, sol_dif_ln_hires ,&
           sr_dir_ln_hires , sr_dif_ln_hires  ,&
           reflectance_out , transmittance_out,&
#endif
           !Ozone stress variables
           o3coefv_sun,  o3coefv_sha,  o3coefg_sun,  o3coefg_sha,  &
           lai_old,      o3uptakesun,  o3uptakesha,  forc_ozone,   &
           !End ozone stress variables
           !WUE stomata model parameter
           lambda,                                                 &
           !End WUE stomata model parameter
           zwt,          wdsrf,        wa,           wetwat,       &
           t_lake,       lake_icefrac, savedtke1,    &

         ! SNICAR snow model related
           snw_rds,      ssno_lyr,     &
           mss_bcpho,    mss_bcphi,    mss_ocpho,    mss_ocphi,    &
           mss_dst1,     mss_dst2,     mss_dst3,     mss_dst4,     &

         ! additional diagnostic variables for output
           laisun,       laisha,       rootr,        rootflux,     &
           rstfacsun_out,rstfacsha_out,gssun_out,    gssha_out,    &
           assimsun_out, etrsun_out,   assimsha_out, etrsha_out,   &
           h2osoi,       wat,          rss,          &

         ! FLUXES
           taux,         tauy,         fsena,        fevpa,        &
           lfevpa,       fsenl,        fevpl,        etr,          &
           fseng,        fevpg,        olrg,         fgrnd,        &
           trad,         tref,         qref,         t2m_wmo,      &
           frcsat,       rsur,         rsur_se,      rsur_ie,      &
           rsub,                                                   &
           rnof,         qintr,        qinfl,        qlayer,       &
           lake_deficit, qdrip,        rst,          assim,        &
           respc,        sabvsun,      sabvsha,      sabg,         &
           sr,           solvd,        solvi,        solnd,        &
           solni,        srvd,         srvi,         srnd,         &
           srni,         solvdln,      solviln,      solndln,      &
           solniln,      srvdln,       srviln,       srndln,       &
           srniln,       qcharge,      xerr,         zerr,         &

         ! TUNABLE model constants
           zlnd,         zsno,         csoilc,       dewmx,        &
           ! 'wtfact' is updated to gridded 'fsatmax' data.
           capr,         cnfac,        ssi,          wimp,         &
           pondmx,       smpmax,       smpmin,       trsmx0,       &
           tcrit,        &

         ! additional variables required by coupling with WRF model
           emis,         z0m,          zol,          rib,          &
           ustar,        qstar,        tstar,        fm,           &
           fh,           fq                                        )

!=======================================================================
!
!  Main subroutine, advance time information
!
!  Initial : Yongjiu Dai, 1999-2014
!  Revised : Hua Yuan, Shupeng Zhang, Nan Wei, Xingjie Lu, Zhongwang Wei, Yongjiu Dai
!            2014-2024
!
!     FLOW DIAGRAM FOR CoLMMAIN
!
!     CoLMMAIN ===>netsolar                 |> all surface
!                  rain_snow_temp           !> all surface
!
!                  LEAF_interception        |]
!                  newsnow                  |] patchtype = 0 (soil ground)
!                  THERMAL                  |]           = 1 (urban & built-up)
!                  WATER                    |]           = 2 (wetland)
!                  snowcompaction           |]           = 3 (land ice)
!                  snowlayerscombine        |]           = 4 (lake)
!                  snowlayersdivide         |]
!
!                  GLACIER_TEMP             |] glacier model
!                  GLACIER_WATER            |]
!
!                  newsnow_lake             |]
!                  laketem                  |] lake scheme
!                  snowwater_lake           |]
!
!                  SOCEAN                   |> ocean and sea ice
!
!                  orb_coszen               |> all surface
!                  EcoModel (LAI_empirical) |> land - not actived
!                  snowfraction             |> land
!                  albland                  |> land
!                  albocean                 |> ocean & sea ice
!
!=======================================================================

   USE MOD_Precision
   USE MOD_Vars_Global
   USE MOD_Const_Physical, only: tfrz, denh2o, denice, cpliq, cpice
   USE MOD_Vars_TimeVariables, only: tlai, tsai, waterstorage
#if (defined LULC_IGBP_PFT || defined LULC_IGBP_PC)
   USE MOD_LandPFT, only: patch_pft_s, patch_pft_e
   USE MOD_Vars_PFTimeInvariants, only: pftfrac
   USE MOD_Vars_PFTimeVariables, only: tlai_p, lai_p, tsai_p, sai_p, sigf_p
#endif
   USE MOD_RainSnowTemp, only: rain_snow_temp
#ifdef HYPERSPECTRAL
   USE MOD_NetSolar_Hyper, only: netsolar_hyper
#else
   USE MOD_NetSolar, only: netsolar
#endif
   USE MOD_OrbCoszen, only: orb_coszen
   USE MOD_NewSnow, only: newsnow
   USE MOD_Thermal, only: THERMAL
   USE MOD_SoilSnowHydrology, only: WATER_2014, WATER_VSF
   USE MOD_SnowFraction, only: snowfraction, snowfraction_pftwrap
   USE MOD_SnowLayersCombineDivide, only: snowcompaction, snowlayerscombine, &
      snowlayerscombine_snicar, snowlayersdivide, snowlayersdivide_snicar
   USE MOD_Glacier, only: GLACIER_TEMP, GLACIER_WATER, GLACIER_WATER_snicar
   USE MOD_Lake, only: newsnow_lake, laketem, snowwater_lake, adjust_lake_layer
   USE MOD_SimpleOcean, only: SOCEAN
#ifdef HYPERSPECTRAL
   USE MOD_Albedo_hires, only: albland_HiRes, albocean
   USE MOD_HighRes_Parameters, only: get_loc_params
#else
   USE MOD_Albedo, only: albland, albocean
#endif
   USE MOD_LAIEmpirical, only: LAI_empirical
   USE MOD_TimeManager
   USE MOD_Namelist, only: DEF_Interception_scheme, DEF_USE_VariablySaturatedFlow, DEF_USE_PLANTHYDRAULICS, DEF_USE_IRRIGATION, DEF_SPLIT_SOILSNOW, DEF_USE_Dynamic_Wetland, DEF_VEG_SNOW
#ifdef TRACER
   USE MOD_Tracer_Precip,       only: tracer_precip
#endif
#ifdef TRACER
   USE MOD_Tracer_Evapo,        only: tracer_evapo
#endif
#ifdef TRACER
   USE MOD_Tracer_SoilWater,    only: tracer_soil_water, tracer_wetland
#endif
   ! tracer_snow_layer_adj was replaced by tracer-aware combine/divide:
   ! snowlayerscombine / snowlayersdivide (and SNICAR variants) now carry
   ! trc_wliq / trc_wice / trc_scv through the same per-layer topology as
   ! the water side, so no post-hoc redistribution is needed.
#ifdef TRACER
   USE MOD_Tracer_Snow,         only: tracer_newsnow
#endif

#ifdef TRACER
   USE MOD_Tracer_Conservation, only: tracer_save_storage, tracer_balance_check, &
      tracer_apply_reactive_processes
#endif
#ifdef TRACER
   USE MOD_Tracer_Hist,         only: tracer_hist_accumulate
#endif
#ifdef TRACER
   USE MOD_Tracer_Vars,         only: trc_wliq_soisno, trc_wice_soisno, trc_scv
#endif
   USE MOD_LeafInterception, only: LEAF_interception_wrap, LEAF_interception_pftwrap
#if (defined CaMa_Flood)
   ! get flood depth [mm], flood fraction[0-1], flood evaporation [mm/s], flood inflow [mm/s]
   USE MOD_CaMa_colmCaMa, only: get_fldevp
   USE YOS_CMF_INPUT, only: LWINFILT,LWEVAP
#endif
#ifdef CROP
   USE MOD_Irrigation, only: CalIrrigationApplicationFluxes
#endif
   USE MOD_SPMD_Task

#ifdef EXTERNAL_LAKE
   USE MOD_Lake_Driver, only: external_lake
#endif

   IMPLICIT NONE

!-------------------------- Dummy Arguments ----------------------------
   real(r8),intent(in) :: deltim  !seconds in a time step [second]
   logical, intent(in) :: doalb   !true if time for surface albedo calculation
   logical, intent(in) :: dolai   !true if time for leaf area index calculation
   logical, intent(in) :: dosst   !true to update sst/ice/snow before calculation

   integer, intent(in) :: &
        ipatch        ! patch index

   real(r8), intent(in) :: &
        patchlonr   ,&! longitude in radians
        patchlatr     ! latitude in radians

   integer, intent(in) :: &
        patchclass  ,&! land patch class of USGS classification or others
        patchtype     ! land patch type (0=soil, 1=urban and built-up,
                      ! 2=wetland, 3=land ice, 4=land water bodies, 99 = ocean)

   real(r8), intent(in)    :: lakedepth         ! lake depth (m)
   real(r8), intent(inout) :: dz_lake(nl_lake)  ! lake layer thickness (m)

   real(r8), intent(in) :: &
        elvstd               ,&! standard deviation of elevation (m)
        BVIC                 ,&! vic model parameter b

        ! soil physical parameters and lake info
        soil_s_v_alb         ,&! albedo of visible of the saturated soil
        soil_d_v_alb         ,&! albedo of visible of the dry soil
        soil_s_n_alb         ,&! albedo of near infrared of the saturated soil
        soil_d_n_alb         ,&! albedo of near infrared of the dry soil

        vf_quartz  (nl_soil) ,&! volumetric fraction of quartz within mineral soil
        vf_gravels (nl_soil) ,&! volumetric fraction of gravels
        vf_om      (nl_soil) ,&! volumetric fraction of organic matter
        vf_sand    (nl_soil) ,&! volumetric fraction of sand
        wf_gravels (nl_soil) ,&! gravimetric fraction of gravels
        wf_sand    (nl_soil) ,&! gravimetric fraction of sand
        porsl      (nl_soil) ,&! fraction of soil that is voids [-]
        psi0       (nl_soil) ,&! minimum soil suction [mm]
        bsw        (nl_soil) ,&! clapp and hornberger "b" parameter [-]
        theta_r  (1:nl_soil) ,&! residual water content (cm3/cm3)
        fsatmax              ,&! maximum saturated area fraction [-]
        fsatdcf              ,&! decay factor in calculation of saturated area fraction [1/m]
        topoweti             ,&! mean topographic wetness index
        alp_twi              ,&! alpha in three parameter gamma distribution of twi
        chi_twi              ,&! chi   in three parameter gamma distribution of twi
        mu_twi               ,&! mu    in three parameter gamma distribution of twi
#ifdef vanGenuchten_Mualem_SOIL_MODEL
        alpha_vgm(1:nl_soil) ,&! parameter corresponding approximately to inverse of air-entry value
        n_vgm    (1:nl_soil) ,&! a shape parameter
        L_vgm    (1:nl_soil) ,&! pore-connectivity parameter
        sc_vgm   (1:nl_soil) ,&! saturation at air entry value in classical vanGenuchten model [-]
        fc_vgm   (1:nl_soil) ,&! a scaling factor by using air entry value in the Mualem model [-]
#endif
        hksati     (nl_soil) ,&! hydraulic conductivity at saturation [mm h2o/s]
        csol       (nl_soil) ,&! heat capacity of soil solids [J/(m3 K)]
        k_solids   (nl_soil) ,&! thermal conductivity of minerals soil [W/m-K]
        dksatu     (nl_soil) ,&! thermal conductivity of saturated unfrozen soil [W/m-K]
        dksatf     (nl_soil) ,&! thermal conductivity of saturated frozen soil [W/m-K]
        dkdry      (nl_soil) ,&! thermal conductivity for dry soil  [J/(K s m)]
        BA_alpha   (nl_soil) ,&! alpha in Balland and Arp(2005) thermal conductivity scheme
        BA_beta    (nl_soil) ,&! beta in Balland and Arp(2005) thermal conductivity scheme
        rootfr     (nl_soil) ,&! fraction of roots in each soil layer

        ! vegetation static, dynamic, derived parameters
        htop        ,&! canopy top height [m]
        hbot        ,&! canopy bottom height [m]
        sqrtdi      ,&! inverse sqrt of leaf dimension [m**-0.5]
        effcon      ,&! quantum efficiency of RuBP regeneration (mol CO2/mol quanta)
        vmax25      ,&! maximum carboxylation rate at 25 C at canopy top
        kmax_sun    ,&! Plant Hydraulics Parameters
        kmax_sha    ,&! Plant Hydraulics Parameters
        kmax_xyl    ,&! Plant Hydraulics Parameters
        kmax_root   ,&! Plant Hydraulics Parameters
        psi50_sun   ,&! water potential at 50% loss of sunlit leaf tissue conductance (mmH2O)
        psi50_sha   ,&! water potential at 50% loss of shaded leaf tissue conductance (mmH2O)
        psi50_xyl   ,&! water potential at 50% loss of xylem tissue conductance (mmH2O)
        psi50_root  ,&! water potential at 50% loss of root tissue conductance (mmH2O)
        ck          ,&! shape-fitting parameter for vulnerability curve (-)
        slti        ,&! slope of low temperature inhibition function      [s3]
        hlti        ,&! 1/2 point of low temperature inhibition function  [s4]
        shti        ,&! slope of high temperature inhibition function     [s1]
        hhti        ,&! 1/2 point of high temperature inhibition function [s2]
        trda        ,&! temperature coefficient in gs-a model             [s5]
        trdm        ,&! temperature coefficient in gs-a model             [s6]
        trop        ,&! temperature coefficient in gs-a model
        g1          ,&! conductance-photosynthesis slope parameter for medlyn model
        g0          ,&! conductance-photosynthesis intercept for medlyn model
        gradm       ,&! conductance-photosynthesis slope parameter
        binter      ,&! conductance-photosynthesis intercep
        extkn       ,&! coefficient of leaf nitrogen allocation
        chil        ,&! leaf angle distribution factor
        rho(2,2)    ,&! leaf reflectance (iw=iband, il=life and dead)
        tau(2,2)    ,&! leaf transmittance (iw=iband, il=life and dead)
#ifdef HYPERSPECTRAL
        ! hyperspectral scheme parameters
        clr_frac     ( 211, 90, 5 ),&
        cld_frac     ( 211, 5 )    ,&
        reflectance  ( 0:15, 211, 2 ),&
        transmittance( 0:15, 211, 2 ),&
        soil_alb     ( 211 )       ,&
        kw           ( 211 )       ,&
        nw           ( 211 )       ,&
#endif

        ! tunable parameters
        zlnd        ,&! roughness length for soil [m]
        zsno        ,&! roughness length for snow [m]
        csoilc      ,&! drag coefficient for soil under canopy [-]
        dewmx       ,&! maximum dew
        ! wtfact    ,&! (updated to gridded 'fsatmax') fraction of model area with high water table
        capr        ,&! tuning factor to turn first layer T into surface T
        cnfac       ,&! Crank Nicholson factor between 0 and 1
        ssi         ,&! irreducible water saturation of snow
        wimp        ,&! water impermeable if porosity less than wimp
        pondmx      ,&! ponding depth (mm)
        smpmax      ,&! wilting point potential in mm
        smpmin      ,&! restriction for min of soil poten.  (mm)
        trsmx0      ,&! max transpiration for moist soil+100% veg.  [mm/s]
        tcrit         ! critical temp. to determine rain or snow

   integer , intent(in) :: &
        c3c4          ! 1 for C3, 2 for C4

#ifdef HYPERSPECTRAL
   ! Urban hyperspectral albedo
   REAL(r8), ALLOCATABLE :: urban_albedo( :, :, : )    ! (cluster_id, season wavelength)
   REAL(r8), ALLOCATABLE :: mean_albedo ( :, : )       ! (season, wavelength)
   REAL(r8), ALLOCATABLE :: lat_north   ( :    )       ! (cluster_id)
   REAL(r8), ALLOCATABLE :: lat_south   ( :    )       ! (cluster_id)
   REAL(r8), ALLOCATABLE :: lon_east    ( :    )       ! (cluster_id)
   REAL(r8), ALLOCATABLE :: lon_west    ( :    )       ! (cluster_id)

#endif

! Forcing
!-----------------------------------------------------------------------
   real(r8), intent(in) :: &
        forc_pco2m  ,&! partial pressure of CO2 at observational height [pa]
        forc_po2m   ,&! partial pressure of O2 at observational height [pa]
        forc_us     ,&! wind speed in eastward direction [m/s]
        forc_vs     ,&! wind speed in northward direction [m/s]
        forc_t      ,&! temperature at agcm reference height [kelvin]
        forc_q      ,&! specific humidity at agcm reference height [kg/kg]
        forc_prc    ,&! convective precipitation [mm/s]
        forc_prl    ,&! large scale precipitation [mm/s]
        forc_psrf   ,&! atmosphere pressure at the surface [pa]
        forc_pbot   ,&! atmosphere pressure at the bottom of the atmos. model level [pa]
        forc_sols   ,&! atm vis direct beam solar rad onto srf [W/m2]
        forc_soll   ,&! atm nir direct beam solar rad onto srf [W/m2]
        forc_solsd  ,&! atm vis diffuse solar rad onto srf [W/m2]
        forc_solld  ,&! atm nir diffuse solar rad onto srf [W/m2]
#ifdef HYPERSPECTRAL
        forc_solarin,&! atm solar rad onto srf [W/m2]
#endif
        forc_frl    ,&! atmospheric infrared (longwave) radiation [W/m2]
        forc_hgt_u  ,&! observational height of wind [m]
        forc_hgt_t  ,&! observational height of temperature [m]
        forc_hgt_q  ,&! observational height of humidity [m]
        forc_rhoair ,&! density air [kg/m3]
        forc_hpbl   ,&! atmospheric boundary layer height [m]
        forc_aerdep(14)!atmospheric aerosol deposition data [kg/m/s]

#if (defined CaMa_Flood)
   real(r8), intent(in)    :: fldfrc    !inundation fraction
                                        ! --> allow re-evaporation and infiltration![0-1]
   real(r8), intent(inout) :: flddepth  !inundation depth
                                        ! --> allow re-evaporation and infiltration![mm]
   real(r8), intent(out)   :: fevpg_fld !effective evaporation from inundation [mm/s]
   real(r8), intent(out)   :: qinfl_fld !effective re-infiltration from inundation [mm/s]
#endif
! Variables required for restart run
!-----------------------------------------------------------------------
   integer, intent(in) :: &
        idate(3)      ! next time-step /year/julian day/second in a day/

   real(r8), intent(inout) :: oro       ! ocean(0)/seaice(2)/ flag
   real(r8), intent(inout) :: &
        z_sno      (maxsnl+1:0)       ,&! layer depth (m)
        dz_sno     (maxsnl+1:0)       ,&! layer thickness (m)
        t_soisno   (maxsnl+1:nl_soil) ,&! soil + snow layer temperature [K]
        wliq_soisno(maxsnl+1:nl_soil) ,&! liquid water (kg/m2)
        wice_soisno(maxsnl+1:nl_soil) ,&! ice lens (kg/m2)
        hk(1:nl_soil)                 ,&! hydraulic conductivity [mm h2o/s]
        smp(1:nl_soil)                ,&! soil matrix potential [mm]

        t_lake(nl_lake)               ,&! lake temperature (kelvin)
        lake_icefrac(nl_lake)         ,&! lake mass fraction of lake layer that is frozen
        savedtke1                     ,&! top level eddy conductivity (W/m K)
        vegwp(nvegwcs)                ,&! ground surface temperature [k]
        gs0sun                        ,&! working copy of sunlit stomata conductance
        gs0sha                        ,&! working copy of shalit stomata conductance
        !Ozone stress variables
        lai_old     ,&! lai in last time step
        o3uptakesun ,&! Ozone does, sunlit leaf (mmol O3/m^2)
        o3uptakesha ,&! Ozone does, shaded leaf (mmol O3/m^2)
        forc_ozone  ,&
        o3coefv_sun ,&! Ozone stress factor for photosynthesis on sunlit leaf
        o3coefv_sha ,&! Ozone stress factor for photosynthesis on sunlit leaf
        o3coefg_sun ,&! Ozone stress factor for stomata on shaded leaf
        o3coefg_sha ,&! Ozone stress factor for stomata on shaded leaf
        !End ozone stress variables
        !WUE stomata model parameter
        lambda      ,&! Marginal water cost of carbon gain ((mol h2o) (mol co2)-1)
        !WUE stomata model parameter
        t_grnd      ,&! ground surface temperature [k]
        tleaf       ,&! leaf temperature [K]
        ldew        ,&! depth of water on foliage [kg/m2/s]
        ldew_rain   ,&! depth of rain on foliage[kg/m2/s]
        ldew_snow   ,&! depth of snow on foliage[kg/m2/s]
        fwet_snow   ,&! vegetation canopy snow fractional cover [-]
        sag         ,&! non dimensional snow age [-]
        scv         ,&! snow mass (kg/m2)
        snowdp      ,&! snow depth (m)
        zwt         ,&! the depth to water table [m]
        wdsrf       ,&! depth of surface water [mm]
        wa          ,&! water storage in aquifer [mm]
        wetwat      ,&! water storage in wetland [mm]

        snw_rds   ( maxsnl+1:0 )      ,&! effective grain radius (col,lyr) [microns, m-6]
        mss_bcpho ( maxsnl+1:0 )      ,&! mass of hydrophobic BC in snow  (col,lyr) [kg]
        mss_bcphi ( maxsnl+1:0 )      ,&! mass of hydrophillic BC in snow (col,lyr) [kg]
        mss_ocpho ( maxsnl+1:0 )      ,&! mass of hydrophobic OC in snow  (col,lyr) [kg]
        mss_ocphi ( maxsnl+1:0 )      ,&! mass of hydrophillic OC in snow (col,lyr) [kg]
        mss_dst1  ( maxsnl+1:0 )      ,&! mass of dust species 1 in snow  (col,lyr) [kg]
        mss_dst2  ( maxsnl+1:0 )      ,&! mass of dust species 2 in snow  (col,lyr) [kg]
        mss_dst3  ( maxsnl+1:0 )      ,&! mass of dust species 3 in snow  (col,lyr) [kg]
        mss_dst4  ( maxsnl+1:0 )      ,&! mass of dust species 4 in snow  (col,lyr) [kg]
        ssno_lyr  (2,2,maxsnl+1:1)    ,&! snow layer absorption [-]

        fveg        ,&! fraction of vegetation cover
        fsno        ,&! fractional snow cover
        sigf        ,&! fraction of veg cover, excluding snow-covered veg [-]
        green       ,&! greenness
        lai         ,&! leaf area index
        sai         ,&! stem area index
#ifdef HYPERSPECTRAL
        alb_hires(211, 2),& ! hyperspectral albedo
#endif

        coszen      ,&! cosine of solar zenith angle
        alb(2,2)    ,&! averaged albedo [-]
        ssun(2,2)   ,&! sunlit canopy absorption for solar radiation
        ssha(2,2)   ,&! shaded canopy absorption for solar radiation
        ssoi(2,2)   ,&! ground soil absorption [-]
        ssno(2,2)   ,&! ground snow absorption [-]
        thermk      ,&! canopy gap fraction for tir radiation
        extkb       ,&! (k, g(mu)/mu) direct solar extinction coefficient
        extkd         ! diffuse and scattered diffuse PAR extinction coefficient


! additional diagnostic variables for output
   real(r8), intent(out) :: &
        laisun           ,&! sunlit leaf area index
        laisha           ,&! shaded leaf area index
        rstfacsun_out    ,&! factor of soil water stress
        rstfacsha_out    ,&! factor of soil water stress
        gssun_out        ,&! sunlit stomata conductance
        gssha_out        ,&! shaded stomata conductance
        wat              ,&! total water storage
        rss              ,&! soil surface resistance [s/m]
        rootr(nl_soil)   ,&! water uptake fraction from different layers, all layers add to 1.0
        rootflux(nl_soil),&! water exchange between soil and root in different layers
                           ! Positive: soil->root[?]
#ifdef HYPERSPECTRAL
        reflectance_out  (211, 0:15)  ,&! high resolution reflectance
        transmittance_out(211, 0:15)  ,&! high resolution transmittance
#endif
        h2osoi(nl_soil)  ,&! volumetric soil water in layers [m3/m3]
        qlayer(0:nl_soil),&! water flux at between soil layer [mm h2o/s]
        lake_deficit       ! lake deficit due to evaporation (mm h2o/s)

   real(r8), intent(out) :: &
        assimsun_out,&
        etrsun_out  ,&
        assimsha_out,&
        etrsha_out
! Fluxes
!-----------------------------------------------------------------------
   real(r8), intent(out) :: &
        taux        ,&! wind stress: E-W [kg/m/s**2]
        tauy        ,&! wind stress: N-S [kg/m/s**2]
        fsena       ,&! sensible heat from canopy height to atmosphere [W/m2]
        fevpa       ,&! evapotranspiration from canopy height to atmosphere [mm/s]
        lfevpa      ,&! latent heat flux from canopy height to atmosphere [W/2]
        fsenl       ,&! sensible heat from leaves [W/m2]
        fevpl       ,&! evaporation+transpiration from leaves [mm/s]
        etr         ,&! transpiration rate [mm/s]
        fseng       ,&! sensible heat flux from ground [W/m2]
        fevpg       ,&! evaporation heat flux from ground [mm/s]
        olrg        ,&! outgoing long-wave radiation from ground+canopy
        fgrnd       ,&! ground heat flux [W/m2]
        xerr        ,&! water balance error at current time-step [mm/s]
        zerr        ,&! energy balance error at current time-step [W/m2]

        tref        ,&! 2 m height air temperature [K]
        qref        ,&! 2 m height air specific humidity
        t2m_wmo     ,&! 2 m wmo std air temperature [K]
        trad        ,&! radiative temperature [K]
        frcsat      ,&! fraction of saturation area
        rsur        ,&! surface runoff (mm h2o/s)
        rsur_se     ,&! saturation excess surface runoff (mm h2o/s)
        rsur_ie     ,&! infiltration excess surface runoff (mm h2o/s)
        rsub        ,&! subsurface runoff (mm h2o/s)
        rnof        ,&! total runoff (mm h2o/s)
        qintr       ,&! interception (mm h2o/s)
        qinfl       ,&! infiltration (mm h2o/s)
        qdrip       ,&! throughfall (mm h2o/s)
        qcharge     ,&! groundwater recharge [mm/s]

        rst         ,&! canopy stomatal resistance
        assim       ,&! canopy assimilation
        respc       ,&! canopy respiration

        sabvsun     ,&! solar absorbed by sunlit vegetation [W/m2]
        sabvsha     ,&! solar absorbed by shaded vegetation [W/m2]
        sabg        ,&! solar absorbed by ground  [W/m2]
        sr          ,&! total reflected solar radiation (W/m2)
        solvd       ,&! incident direct beam vis solar radiation (W/m2)
        solvi       ,&! incident diffuse beam vis solar radiation (W/m2)
        solnd       ,&! incident direct beam nir solar radiation (W/m2)
        solni       ,&! incident diffuse beam nir solar radiation (W/m2)
        srvd        ,&! reflected direct beam vis solar radiation (W/m2)
        srvi        ,&! reflected diffuse beam vis solar radiation (W/m2)
        srnd        ,&! reflected direct beam nir solar radiation (W/m2)
        srni        ,&! reflected diffuse beam nir solar radiation (W/m2)
        solvdln     ,&! incident direct beam vis solar radiation at local noon(W/m2)
        solviln     ,&! incident diffuse beam vis solar radiation at local noon(W/m2)
        solndln     ,&! incident direct beam nir solar radiation at local noon(W/m2)
        solniln     ,&! incident diffuse beam nir solar radiation at local noon(W/m2)
        srvdln      ,&! reflected direct beam vis solar radiation at local noon(W/m2)
        srviln      ,&! reflected diffuse beam vis solar radiation at local noon(W/m2)
        srndln      ,&! reflected direct beam nir solar radiation at local noon(W/m2)
        srniln      ,&! reflected diffuse beam nir solar radiation at local noon(W/m2)
#ifdef HYPERSPECTRAL
        sol_dir_ln_hires(211)  ,&! incident direct beam vis solar radiation at local noon(W/m2)
        sol_dif_ln_hires(211)  ,&! incident diffuse beam vis solar radiation at local noon(W/m2)
        sr_dir_ln_hires(211)   ,&! reflected direct beam nir solar radiation at local noon(W/m2)
        sr_dif_ln_hires(211)   ,&! reflected diffuse beam nir solar radiation at local noon(W/m2)
#endif

        forc_rain   ,&! rain [mm/s]
        forc_snow   ,&! snow [mm/s]

        emis        ,&! averaged bulk surface emissivity
        z0m         ,&! effective roughness [m]
        zol         ,&! dimensionless height (z/L) used in Monin-Obukhov theory
        rib         ,&! bulk Richardson number in surface layer
        ustar       ,&! u* in similarity theory [m/s]
        qstar       ,&! q* in similarity theory [kg/kg]
        tstar       ,&! t* in similarity theory [K]
        fm          ,&! integral of profile function for momentum
        fh          ,&! integral of profile function for heat
        fq            ! integral of profile function for moisture

!-------------------------- Local Variables ----------------------------
   logical  :: is_dry_lake

   real(r8) :: &
        calday      ,&! Julian cal day (1.xx to 365.xx)
        endwb       ,&! water mass at the end of time step
        errore      ,&! energy balance error (Wm-2)
        errorw      ,&! water balance error (mm)
        fiold(maxsnl+1:nl_soil), &! fraction of ice relative to the total water
        w_old       ,&! liquid water mass of the column at the previous time step (mm)

        sabg_soil   ,&! solar absorbed by soil fraction
        sabg_snow   ,&! solar absorbed by snow fraction
        parsun      ,&! PAR by sunlit leaves [W/m2]
        parsha      ,&! PAR by shaded leaves [W/m2]
        qseva       ,&! ground surface evaporation rate (mm h2o/s)
        qsdew       ,&! ground surface dew formation (mm h2o /s) [+]
        qsubl       ,&! sublimation rate from snow pack (mm h2o /s) [+]
        qfros       ,&! surface dew added to snow pack (mm h2o /s) [+]
        qseva_soil  ,&! ground soil surface evaporation rate (mm h2o/s)
        qsdew_soil  ,&! ground soil surface dew formation (mm h2o /s) [+]
        qsubl_soil  ,&! sublimation rate from soil ice pack (mm h2o /s) [+]
        qfros_soil  ,&! surface dew added to soil ice pack (mm h2o /s) [+]
        qseva_snow  ,&! ground snow surface evaporation rate (mm h2o/s)
        qsdew_snow  ,&! ground snow surface dew formation (mm h2o /s) [+]
        qsubl_snow  ,&! sublimation rate from snow pack (mm h2o /s) [+]
        qfros_snow  ,&! surface dew added to snow pack (mm h2o /s) [+]
        scvold      ,&! snow cover for previous time step [mm]
        sm          ,&! rate of snowmelt [kg/(m2 s)]
        ssw         ,&! water volumetric content of soil surface layer [m3/m3]
        tssub(7)    ,&! surface/sub-surface temperatures [K]
        tssea       ,&! sea surface temperature [K]
        totwb       ,&! water mass at the beginning of time step
        wt          ,&! fraction of vegetation buried (covered) by snow [-]
        z_soisno (maxsnl+1:nl_soil), &! layer depth (m)
        dz_soisno(maxsnl+1:nl_soil), &! layer thickness (m)
        zi_soisno(maxsnl  :nl_soil)   ! interface level below a "z" level (m)

   real(r8) :: &
        prc_rain    ,&! convective rainfall [kg/(m2 s)]
        prc_snow    ,&! convective snowfall [kg/(m2 s)]
        prl_rain    ,&! large scale rainfall [kg/(m2 s)]
        prl_snow    ,&! large scale snowfall [kg/(m2 s)]
        t_precip    ,&! snowfall/rainfall temperature [kelvin]
        bifall      ,&! bulk density of newly fallen dry snow [kg/m3]
        pg_rain     ,&! rainfall onto ground including canopy runoff [kg/(m2 s)]
        pg_snow     ,&! snowfall onto ground including canopy runoff [kg/(m2 s)]
        qintr_rain  ,&! rainfall interception (mm h2o/s)
        qintr_snow  ,&! snowfall interception (mm h2o/s)
        gross_intr_rain ,&! gross rain entering canopy pool (mm h2o/s, >=0)
        gross_intr_snow ,&! gross snow entering canopy pool (mm h2o/s, >=0)
        xsc_rain_out    ,&! pre-mix rain release from old pool (mm h2o/s, >=0)
        xsc_snow_out      ! pre-mix snow release from old pool (mm h2o/s, >=0)

#ifdef HYPERSPECTRAL
  real(r8) :: &
        dir_frac(211),&! direct beam fraction
        dif_frac(211)  ! diffuse beam fraction
#endif

   integer snl      ,&! number of snow layers
        imelt(maxsnl+1:nl_soil), &! flag for: melting=1, freezing=2, Nothing happened=0
        lb ,lbsn    ,&! lower bound of arrays
        j             ! do looping index

   ! For SNICAR snow model
   !----------------------------------------------------------------------
   integer  snl_bef                    !number of snow layers
   real(r8) forc_aer           ( 14 )  !aerosol deposition from atmosphere (grd,aer) [kg m-1 s-1]
   real(r8) snofrz       (maxsnl+1:0)  !snow freezing rate (col,lyr) [kg m-2 s-1]
   real(r8) t_soisno_    (maxsnl+1:1)  !soil + snow layer temperature [K]
   real(r8) dz_soisno_   (maxsnl+1:1)  !layer thickness (m)
   real(r8) sabg_snow_lyr(maxsnl+1:1)  !snow layer absorption [W/m-2]
   !----------------------------------------------------------------------
   !  For irrigation
   !----------------------------------------------------------------------
   real(r8) :: qflx_irrig_drip         ! drip irrigation rate [mm/s]
   real(r8) :: qflx_irrig_sprinkler    ! sprinkler irrigation rate [mm/s]
   real(r8) :: qflx_irrig_flood        ! flood irrigation rate [mm/s]
   real(r8) :: qflx_irrig_paddy        ! paddy irrigation rate [mm/s]
   !----------------------------------------------------------------------
   real(r8) :: a, aa, gwat
   real(r8) :: wextra, t_rain, t_snow
   integer ps, pe, pc

   ! Tracer local variables
   real(r8) :: xerr_tracer
   real(r8), allocatable :: wliq_soisno_old_trc(:)
   real(r8), allocatable :: wice_soisno_old_trc(:)
   real(r8) :: wa_old_trc, wdsrf_old_trc, wetwat_old_trc
   real(r8) :: ldew_rain_old_trc, ldew_snow_old_trc   ! before LEAF_INTERCEPTION
   ! Canopy phase-change mass transferred by LEAF_INTERCEPTION this step
   ! [grid-scale mm, >=0]. ldew_smelt_out moves canopy ldew_snow → ldew_rain
   ! (melt); ldew_frzc_out moves canopy ldew_rain → ldew_snow (freeze).
   ! Passed to tracer_precip so trc_ldew_snow and trc_ldew_rain migrate
   ! coherently with their bulk water pools. Non-zero only under
   ! NoahMP/MATSIRO/VIC/JULES schemes.
   real(r8) :: ldew_smelt_trc, ldew_frzc_trc
   ! Canopy phase-change fusion heat flux exported by LEAF_INTERCEPTION
   ! [W/m^2; +heats canopy, -cools canopy]. Passed to THERMAL so the canopy
   ! energy balance (and errore) includes the fusion heat absorbed by
   ! melt / released by freeze that happened inside LeafInterception for
   ! schemes 4/5/6/7. Prior to this path, the ROLLBACK NM-1 audit left
   ! the heat unaccounted, biasing tleaf warm and inflating Ec/ET.
   real(r8) :: canopy_phase_heat
#if (defined LULC_IGBP_PFT || defined LULC_IGBP_PC)
   real(r8), allocatable :: canopy_phase_heat_p(:)
#endif
   ! Explicit canopy phase-change mass exported by THERMAL.
   ! Forwarded into tracer_evapo so it stops inferring the phase amount
   ! from the d_rain/d_snow sign-pattern heuristic (which under-counts
   ! when canopy melt and rain-pool evap coexist in one step).
   real(r8) :: canopy_smelt_mass_th, canopy_frzc_mass_th
   real(r8), allocatable :: soil_thaw_mass_th(:), soil_frzc_mass_th(:)
   real(r8) :: ldew_rain_bef_th, ldew_snow_bef_th    ! before THERMAL
   real(r8) :: scv_bef_trc                            ! pre-newsnow scv
   real(r8), allocatable :: wice_snow_bef_trc(:)   ! pre-newsnow wice for tracer_newsnow Case C
   real(r8) :: glacier_overflow_mass_trc
   ! Per-layer transpiration demand returned by WATER_VSF so the tracer path
   ! can subtract the matching tracer mass from each soil layer. Only used
   ! for the soil-ground branches; the wetland path ignores this and handles
   ! etr inside the mixed pool of tracer_wetland.
   real(r8) :: etroot_trc(nl_soil)
   ! Per-layer ice withdrawn inside WATER_VSF to close the ET water-balance
   ! deficit (wblc > 0 branch). Tracer path removes the matching trc_wice
   ! and accounts it as evaporation; see tracer_soil_water Section 5b.
   real(r8) :: wblc_ice_sink_trc(nl_soil)
   ! Actual per-layer ET water (mm) after the deficit cascade in
   ! soil_water_vertical_movement, and the aquifer share absorbing any
   ! remaining deficit (mm). Tracer path mirrors these removals so
   ! cascade-driven ET does not leave tracer behind in a dry layer or
   ! miss the aquifer withdrawal entirely.
   real(r8) :: etroot_actual_trc(nl_soil)
   real(r8) :: etroot_aquifer_trc
   real(r8) :: waterstorage_trc_beg
   real(r8) :: waterstorage_trc_ground

#if (defined CaMa_Flood)
   !add variables for flood evaporation [mm/s] and re-infiltration [mm/s] calculation.
   real(r8) :: kk
   real(r8) :: taux_fld    ! wind stress: E-W [kg/m/s**2]
   real(r8) :: tauy_fld    ! wind stress: N-S [kg/m/s**2]
   real(r8) :: fsena_fld   ! sensible heat from agcm reference height to atmosphere [W/m2]
   real(r8) :: fevpa_fld   ! evaporation from agcm reference height to atmosphere [mm/s]
   real(r8) :: fseng_fld   ! sensible heat flux from ground [W/m2]
   real(r8) :: tref_fld    ! 2 m height air temperature [kelvin]
   real(r8) :: qref_fld    ! 2 m height air humidity
   real(r8) :: z0m_fld     ! effective roughness [m]
   real(r8) :: zol_fld     ! dimensionless height (z/L) used in Monin-Obukhov theory
   real(r8) :: rib_fld     ! bulk Richardson number in surface layer
   real(r8) :: ustar_fld   ! friction velocity [m/s]
   real(r8) :: tstar_fld   ! temperature scaling parameter
   real(r8) :: qstar_fld   ! moisture scaling parameter
   real(r8) :: fm_fld      ! integral of profile function for momentum
   real(r8) :: fh_fld      ! integral of profile function for heat
   real(r8) :: fq_fld      ! integral of profile function for moisture
#endif

!-----------------------------------------------------------------------

      z_soisno (maxsnl+1:0) = z_sno (maxsnl+1:0)
      z_soisno (1:nl_soil ) = z_soi (1:nl_soil )
      dz_soisno(maxsnl+1:0) = dz_sno(maxsnl+1:0)
      dz_soisno(1:nl_soil ) = dz_soi(1:nl_soil )

      ! SNICAR initialization
      ! ---------------------

      ! snow freezing rate (col,lyr) [kg m-2 s-1]
      snofrz(:) = 0.

      ! aerosol deposition value
      IF (DEF_Aerosol_Readin) THEN
         forc_aer(:) = forc_aerdep   ! read from outside forcing file
      ELSE
         forc_aer(:) = 0.            ! manual setting
      ENDIF


!======================================================================
!  [1] Solar absorbed by vegetation and ground
!      and precipitation information (rain/snow fall and precip temperature
!======================================================================
#ifdef HYPERSPECTRAL
      CALL get_loc_params(forc_solarin, idate, coszen, patchlatr, patchlonr, clr_frac, cld_frac, dir_frac, dif_frac)

      CALL netsolar_hyper (ipatch,idate,deltim,patchlonr,patchtype,&
                     forc_sols,forc_soll,forc_solsd,forc_solld,&
                     alb,ssun,ssha,lai,sai,rho,tau,ssoi,ssno,ssno_lyr,fsno,&
                     parsun,parsha,sabvsun,sabvsha,sabg,sabg_soil,sabg_snow,sabg_snow_lyr,&
                     sr,solvd,solvi,solnd,solni,srvd,srvi,srnd,srni,&
                     solvdln,solviln,solndln,solniln,srvdln,srviln,srndln,srniln,&
                     ! new variables for hyperspectral scheme
                     dir_frac, dif_frac, alb_hires    ,&
                     sol_dir_ln_hires,sol_dif_ln_hires,&
                     sr_dir_ln_hires ,sr_dif_ln_hires  )

#else
      CALL netsolar (ipatch,idate,deltim,patchlonr,patchtype,&
                     forc_sols,forc_soll,forc_solsd,forc_solld,&
                     alb,ssun,ssha,lai,sai,rho,tau,ssoi,ssno,ssno_lyr,fsno,&
                     parsun,parsha,sabvsun,sabvsha,sabg,sabg_soil,sabg_snow,sabg_snow_lyr,&
                     sr,solvd,solvi,solnd,solni,srvd,srvi,srnd,srni,&
                     solvdln,solviln,solndln,solniln,srvdln,srviln,srndln,srniln)
#endif

      CALL rain_snow_temp (patchtype, &
                           forc_t,forc_q,forc_psrf,forc_prc,forc_prl,forc_us,forc_vs,tcrit,&
                           prc_rain,prc_snow,prl_rain,prl_snow,t_precip,bifall)

      forc_rain = prc_rain + prl_rain
      forc_snow = prc_snow + prl_snow

#ifdef TRACER
            ldew_rain_old_trc = ldew_rain
            ldew_snow_old_trc = ldew_snow
            ! NOTE: tracer_save_storage moved below, after snl is recomputed (line ~771)
#endif

!======================================================================

      is_dry_lake = DEF_USE_Dynamic_Lake .and. (patchtype == 4) .and. &
                    ((wdsrf < 100.) .or. (zwt > 0.))


                                                  !         / SOIL GROUND          (patchtype = 0)
      IF ((patchtype <= 2) .or. is_dry_lake) THEN ! <=== is - URBAN and BUILT-UP   (patchtype = 1)
                                                  !         \ WETLAND              (patchtype = 2)
                                                  !           Dry Lake             (patchtype = 4)

! NOTE: PFT and PC are only for soil patches, i.e., patchtype=0.
!======================================================================
                         ! initial set
         scvold = scv    ! snow mass at previous time step

         snl = 0
         DO j=maxsnl+1,0
            IF(wliq_soisno(j)+wice_soisno(j)>0.) snl=snl-1
         ENDDO

         zi_soisno(0)=0.
         IF (snl < 0) THEN
            DO j = -1, snl, -1
               zi_soisno(j)=zi_soisno(j+1)-dz_soisno(j+1)
            ENDDO
         ENDIF
         DO j = 1,nl_soil
            zi_soisno(j)=zi_soisno(j-1)+dz_soisno(j)
         ENDDO

         totwb = ldew + scv + sum(wice_soisno(1:)+wliq_soisno(1:)) + wa
#ifdef CROP
         if(DEF_USE_IRRIGATION) totwb = totwb + waterstorage(ipatch)
#endif
         totwb = totwb + wdsrf
         IF (DEF_USE_VariablySaturatedFlow) THEN
            IF (patchtype == 2) THEN
               totwb = totwb + wetwat
            ENDIF
         ENDIF

         fiold(:) = 0.0
         IF (snl <0 ) THEN
            fiold(snl+1:0)=wice_soisno(snl+1:0)/(wliq_soisno(snl+1:0)+wice_soisno(snl+1:0))
         ENDIF

!----------------------------------------------------------------------
! [2] Irrigation
!----------------------------------------------------------------------
         qflx_irrig_drip = 0._r8
         qflx_irrig_sprinkler = 0._r8
         qflx_irrig_flood = 0._r8
         qflx_irrig_paddy = 0._r8
         waterstorage_trc_beg = 0._r8
#ifdef CROP
         IF (DEF_USE_IRRIGATION) THEN
            waterstorage_trc_beg = max(waterstorage(ipatch), 0._r8)
            IF (patchtype == 0) THEN
               CALL CalIrrigationApplicationFluxes(ipatch,deltim,qflx_irrig_drip, &
                  qflx_irrig_sprinkler,qflx_irrig_flood,qflx_irrig_paddy)
            ENDIF
         ENDIF
#endif
         waterstorage_trc_ground = max(waterstorage_trc_beg - max(qflx_irrig_sprinkler, 0._r8) * deltim, 0._r8)
!----------------------------------------------------------------------
! [3] Canopy interception and precipitation onto ground surface
!----------------------------------------------------------------------
         ! Default zero for patches that skip LEAF_interception (e.g.
         ! water/ice patchtypes that fall through to the non-soil branch)
         ! so tracer_precip / THERMAL never see an undefined value.
         ldew_smelt_trc    = 0._r8
         ldew_frzc_trc     = 0._r8
         canopy_phase_heat = 0._r8
         ! Pre-zero THERMAL's canopy phase-change exports so
         ! a code path that skips THERMAL still surfaces deterministic 0
         ! to tracer_evapo (instead of inheriting last patch's value).
         canopy_smelt_mass_th = 0._r8
         canopy_frzc_mass_th  = 0._r8
#if (defined LULC_IGBP_PFT || defined LULC_IGBP_PC)
         IF (patchtype == 0) THEN
            ps = patch_pft_s(ipatch)
            pe = patch_pft_e(ipatch)
            allocate(canopy_phase_heat_p(ps:pe))
         ELSE
            allocate(canopy_phase_heat_p(1:1))
         ENDIF
         canopy_phase_heat_p(:) = 0._r8
#endif

         IF (patchtype == 0) THEN

#if (defined LULC_USGS || defined LULC_IGBP)
            CALL LEAF_interception_wrap (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,forc_t,&
                      tleaf,prc_rain,prc_snow,prl_rain,prl_snow,qflx_irrig_sprinkler,bifall,&
                      ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,&
                      pg_snow,qintr,qintr_rain,qintr_snow,gross_intr_rain,gross_intr_snow,&
                      xsc_rain_out,xsc_snow_out,&
                      ldew_smelt_trc,ldew_frzc_trc,&
                      canopy_phase_heat)
#endif

#if (defined LULC_IGBP_PFT || defined LULC_IGBP_PC)
            CALL LEAF_interception_pftwrap (ipatch,deltim,dewmx,forc_us,forc_vs,forc_t,&
                      prc_rain,prc_snow,prl_rain,prl_snow,qflx_irrig_sprinkler,bifall,&
                      ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,&
                      pg_snow,qintr,qintr_rain,qintr_snow,gross_intr_rain,gross_intr_snow,&
                      xsc_rain_out,xsc_snow_out,&
                      ldew_smelt_trc,ldew_frzc_trc,&
                      canopy_phase_heat,canopy_phase_heat_p)
#endif

         ELSE
            CALL LEAF_interception_wrap (deltim,dewmx,forc_us,forc_vs,chil,sigf,lai,sai,forc_t,&
                      tleaf,prc_rain,prc_snow,prl_rain,prl_snow,qflx_irrig_sprinkler,bifall,&
                      ldew,ldew_rain,ldew_snow,z0m,forc_hgt_u,pg_rain,&
                      pg_snow,qintr,qintr_rain,qintr_snow,gross_intr_rain,gross_intr_snow,&
                      xsc_rain_out,xsc_snow_out,&
                      ldew_smelt_trc,ldew_frzc_trc,&
                      canopy_phase_heat)
         ENDIF

         qdrip = pg_rain + pg_snow

#ifdef TRACER
            ! Save step-start storage and accumulator snapshots.
            ! Must be AFTER snl recomputation (line 769) and BEFORE tracer_precip.
            ! Pass waterstorage when CROP+DEF_USE_IRRIGATION so the tracer
            ! side includes it in the storage sum (mirrors water-side totwb
            ! at L804-807 / endwb at L1269), making irrigation an internal
            ! transfer instead of a phantom atmospheric input.
#ifdef CROP
            IF (DEF_USE_IRRIGATION) THEN
               CALL tracer_save_storage(ipatch, snl, nl_soil, waterstorage_trc_beg)
            ELSE
               CALL tracer_save_storage(ipatch, snl, nl_soil)
            ENDIF
#else
            CALL tracer_save_storage(ipatch, snl, nl_soil)
#endif

            CALL tracer_precip(ipatch, deltim, &
               forc_rain, forc_snow, qintr, qintr_rain, qintr_snow, &
               pg_rain, pg_snow, ldew_rain, ldew_snow, &
               ldew_rain_old_trc, ldew_snow_old_trc, &
               qflx_irrig_sprinkler, &
               gross_intr_rain, gross_intr_snow, &
               xsc_rain_out, xsc_snow_out, &
               ldew_smelt_trc, ldew_frzc_trc, waterstorage_trc_beg)
#endif

!----------------------------------------------------------------------
! [3] Initialize new snow nodes for snowfall / sleet
!----------------------------------------------------------------------

         snl_bef = snl

         ! Save pre-newsnow state for tracer snowfall tracking
#ifdef TRACER
            IF (.not. allocated(wice_snow_bef_trc)) THEN
               allocate(wice_snow_bef_trc(maxsnl+1:nl_soil))
            ENDIF
            wice_snow_bef_trc = 0._r8
            IF (snl < 0) THEN
               wice_snow_bef_trc(snl+1:0) = wice_soisno(snl+1:0)
            ENDIF
#endif

         CALL newsnow (patchtype,maxsnl,deltim,t_grnd,pg_rain,pg_snow,bifall,&
                       t_precip,zi_soisno(:0),z_soisno(:0),dz_soisno(:0),t_soisno(:0),&
                       wliq_soisno(:0),wice_soisno(:0),fiold(:0),snl,sag,scv,snowdp,fsno,wetwat)

#ifdef TRACER
            ! scv_bef_trc = POST-newsnow scv (includes this step's snowfall).
            ! Used later to detect thin-snow melt during THERMAL.
            scv_bef_trc = scv

            IF (snl < 0) THEN
               CALL tracer_newsnow(ipatch, patchtype, snl, snl_bef, pg_snow, deltim, &
                  scv, scv_bef_trc, wetwat, &
                  wliq_soisno(snl+1:0), wice_soisno(snl+1:0), &
                  wice_snow_bef_trc(snl+1:0))
            ELSE
               CALL tracer_newsnow(ipatch, patchtype, snl, snl_bef, pg_snow, deltim, &
                  scv, scv_bef_trc, wetwat)
            ENDIF
#endif

!----------------------------------------------------------------------
! [4] Energy and Water balance
!----------------------------------------------------------------------
         lb   = snl + 1           !lower bound of array
         lbsn = min(lb,0)

         ! Per-layer phase-change mass arrays must be
         ! allocated unconditionally because the THERMAL call below
         ! always forwards them as keyword args. When DEF_USE_TRACER is
         ! off the values are written but ignored, costing ~few hundred
         ! bytes per patch. Pre-zero so a code path that bypasses meltf
         ! (e.g. patchtype branch that skips GroundTemperature) still
         ! surfaces 0 instead of stale memory.
         allocate(soil_thaw_mass_th(lb:nl_soil), soil_frzc_mass_th(lb:nl_soil))
         soil_thaw_mass_th = 0._r8
         soil_frzc_mass_th = 0._r8

#ifdef TRACER
            allocate(wliq_soisno_old_trc(lb:nl_soil))
            allocate(wice_soisno_old_trc(lb:nl_soil))
            IF (.not. allocated(wice_snow_bef_trc)) allocate(wice_snow_bef_trc(maxsnl+1:nl_soil))
            wliq_soisno_old_trc(lb:nl_soil) = wliq_soisno(lb:nl_soil)
            wice_soisno_old_trc(lb:nl_soil) = wice_soisno(lb:nl_soil)
            wa_old_trc = wa
            wdsrf_old_trc = wdsrf
            wetwat_old_trc = wetwat
            IF (.not. DEF_VEG_SNOW) THEN
               BLOCK
               USE MOD_Tracer_Defs, only: ntracers_loc => ntracers
               USE MOD_Tracer_Vars, only: trc_ldew_rain_loc => trc_ldew_rain, &
                                          trc_ldew_snow_loc => trc_ldew_snow
               integer :: itrc_loc
               ! Single-bucket canopy water is re-phased below; keep tracer
               ! pools in the same phase or evapo leaves hidden snow/rain tracer.
               IF (tleaf > tfrz) THEN
                  DO itrc_loc = 1, ntracers_loc
                     trc_ldew_rain_loc(itrc_loc, ipatch) = trc_ldew_rain_loc(itrc_loc, ipatch) &
                        + trc_ldew_snow_loc(itrc_loc, ipatch)
                     trc_ldew_snow_loc(itrc_loc, ipatch) = 0._r8
                  ENDDO
                  ldew_rain = ldew
                  ldew_snow = 0._r8
               ELSE
                  DO itrc_loc = 1, ntracers_loc
                     trc_ldew_snow_loc(itrc_loc, ipatch) = trc_ldew_snow_loc(itrc_loc, ipatch) &
                        + trc_ldew_rain_loc(itrc_loc, ipatch)
                     trc_ldew_rain_loc(itrc_loc, ipatch) = 0._r8
                  ENDDO
                  ldew_rain = 0._r8
                  ldew_snow = ldew
               ENDIF
               END BLOCK
            ENDIF
            ! Save ldew before THERMAL (for delta-based ET tracking)
            ldew_rain_bef_th = ldew_rain
            ldew_snow_bef_th = ldew_snow
#endif

         CALL THERMAL (ipatch,patchtype,is_dry_lake,lb                ,deltim            ,&
              trsmx0            ,zlnd              ,zsno              ,csoilc            ,&
              dewmx             ,capr              ,cnfac             ,vf_quartz         ,&
              vf_gravels        ,vf_om             ,vf_sand           ,wf_gravels        ,&
              wf_sand           ,csol              ,porsl             ,psi0              ,&
#ifdef Campbell_SOIL_MODEL
              bsw               ,&
#endif
#ifdef vanGenuchten_Mualem_SOIL_MODEL
              theta_r           ,alpha_vgm         ,n_vgm             ,L_vgm             ,&
              sc_vgm            ,fc_vgm            ,&
#endif
              k_solids          ,dksatu            ,dksatf            ,dkdry             ,&
              BA_alpha          ,BA_beta           ,lai               ,laisun            ,&
              laisha            ,sai               ,htop              ,hbot              ,&
              sqrtdi            ,rootfr            ,rstfacsun_out     ,rstfacsha_out     ,&
              rss               ,gssun_out         ,gssha_out         ,assimsun_out      ,&
              etrsun_out        ,assimsha_out      ,etrsha_out        ,&

              effcon            ,vmax25,c3c4       ,hksati            ,smp ,hk           ,&
              kmax_sun          ,kmax_sha          ,kmax_xyl          ,kmax_root         ,&
              psi50_sun         ,psi50_sha         ,psi50_xyl         ,psi50_root        ,&
              ck                ,vegwp             ,gs0sun            ,gs0sha            ,&
              !Ozone stress variables
              o3coefv_sun       ,o3coefv_sha       ,o3coefg_sun       ,o3coefg_sha       ,&
              lai_old           ,o3uptakesun       ,o3uptakesha       ,forc_ozone        ,&
              !End ozone stress variables
              !WUE stomata model parameter
              lambda      ,&! Marginal water cost of carbon gain ((mol h2o) (mol co2)-1)
              !WUE stomata model parameter
              slti              ,hlti              ,shti              ,hhti              ,&
              trda              ,trdm              ,trop              ,g1                ,&
              g0                ,gradm             ,binter            ,extkn             ,&
              forc_hgt_u        ,forc_hgt_t        ,forc_hgt_q        ,forc_us           ,&
              forc_vs           ,forc_t            ,forc_q            ,forc_rhoair       ,&
              forc_psrf         ,forc_pco2m        ,forc_hpbl         ,forc_po2m         ,&
              coszen            ,parsun            ,parsha            ,sabvsun           ,&
              sabvsha           ,sabg              ,sabg_soil         ,sabg_snow         ,&
              forc_frl          ,extkb             ,extkd             ,thermk            ,&
              fsno              ,sigf              ,dz_soisno(lb:)    ,z_soisno(lb:)     ,&
              zi_soisno(lb-1:)  ,tleaf             ,t_soisno(lb:)     ,wice_soisno(lb:)  ,&
              wliq_soisno(lb:)  ,ldew              ,ldew_rain         ,ldew_snow         ,&
              fwet_snow         ,scv               ,snowdp            ,imelt(lb:)        ,&
              taux              ,tauy              ,fsena             ,fevpa             ,&
              lfevpa            ,fsenl             ,fevpl             ,etr               ,&
              fseng             ,fevpg             ,olrg              ,fgrnd             ,&
              rootr             ,rootflux          ,qseva             ,qsdew             ,&
              qsubl             ,qfros             ,qseva_soil        ,qsdew_soil        ,&
              qsubl_soil        ,qfros_soil        ,qseva_snow        ,qsdew_snow        ,&
              qsubl_snow        ,qfros_snow        ,sm                ,tref              ,&
              qref              ,trad              ,rst               ,assim             ,&
              respc             ,errore            ,emis              ,z0m               ,&
              zol               ,rib               ,ustar             ,qstar             ,&
              tstar             ,fm                ,fh                ,fq                ,&
              pg_rain           ,pg_snow           ,t_precip          ,qintr_rain        ,&
#if (defined LULC_IGBP_PFT || defined LULC_IGBP_PC)
              qintr_snow        ,snofrz(lbsn:0)    ,sabg_snow_lyr(lb:1),canopy_phase_heat,canopy_phase_heat_p,&
              canopy_smelt_mass_th = canopy_smelt_mass_th, &
              canopy_frzc_mass_th  = canopy_frzc_mass_th, &
              qphs_thaw_lay_th     = soil_thaw_mass_th, &
              qphs_frzc_lay_th     = soil_frzc_mass_th)
#else
              qintr_snow        ,snofrz(lbsn:0)    ,sabg_snow_lyr(lb:1),canopy_phase_heat  ,&
              canopy_smelt_mass_th = canopy_smelt_mass_th, &
              canopy_frzc_mass_th  = canopy_frzc_mass_th, &
              qphs_thaw_lay_th     = soil_thaw_mass_th, &
              qphs_frzc_lay_th     = soil_frzc_mass_th)
#endif

#ifdef TRACER
            ! soil_thaw_mass_th / soil_frzc_mass_th now hold the exact
            ! layer-internal phase-change mass written by meltf (decoupled
            ! from sublimation/dew). The old d_wice/d_wliq + imelt
            ! heuristic that lived here is no longer needed.

            CALL tracer_evapo(ipatch, deltim, snl, nl_soil, &
               ldew_rain, ldew_snow, ldew_rain_bef_th, ldew_snow_bef_th, &
               wliq_soisno(snl+1:nl_soil), wice_soisno(snl+1:nl_soil), &
               wliq_soisno_old_trc, wice_soisno_old_trc, &
               canopy_smelt_mass_th = canopy_smelt_mass_th, &
               canopy_frzc_mass_th  = canopy_frzc_mass_th, &
               soil_thaw_mass_th = soil_thaw_mass_th, &
               soil_frzc_mass_th = soil_frzc_mass_th, &
               tleaf_frac = tleaf, &
               t_soisno_frac = t_soisno(snl+1:nl_soil), &
               forc_q_frac = forc_q, &
               forc_psrf_frac = forc_psrf)

            ! Sync trc_scv after THERMAL (PhaseChange may have melted thin snow)
            ! When scv decreases, sm is produced and enters gwat. Capture the
            ! tracer that left trc_scv into trc_sm_carry so tracer_soil_water
            ! injects the *actual* scv ratio (not a fixed init-delta R) when
            ! it adds the snowmelt to the surface pool.
            BLOCK
            USE MOD_Tracer_Defs, only: ntracers_loc => ntracers, trc_tiny_loc => trc_tiny
            USE MOD_Tracer_Vars, only: trc_sm_carry_loc => trc_sm_carry
            integer :: itrc_loc
            real(r8) :: ratio_loc
            IF (snl == 0) THEN
               IF (scv < trc_tiny_loc) THEN
                  ! All thin snow melted; entire trc_scv is carried by sm.
                  DO itrc_loc = 1, ntracers_loc
                     trc_sm_carry_loc(itrc_loc, ipatch) = trc_scv(itrc_loc, ipatch)
                     trc_scv(itrc_loc, ipatch) = 0._r8
                  ENDDO
               ELSEIF (scv < scv_bef_trc - trc_tiny_loc) THEN
                  ratio_loc = scv / max(scv_bef_trc, trc_tiny_loc)
                  ratio_loc = max(min(ratio_loc, 1._r8), 0._r8)
                  DO itrc_loc = 1, ntracers_loc
                     trc_sm_carry_loc(itrc_loc, ipatch) = trc_scv(itrc_loc, ipatch) * (1._r8 - ratio_loc)
                     trc_scv(itrc_loc, ipatch) = trc_scv(itrc_loc, ipatch) * ratio_loc
                  ENDDO
               ELSE
                  DO itrc_loc = 1, ntracers_loc
                     trc_sm_carry_loc(itrc_loc, ipatch) = 0._r8
                  ENDDO
               ENDIF
            ELSE
               ! Snow layer present; no thin-snow melt route, melt tracer
               ! comes from trc_wice/trc_wliq via the normal soil_water path.
               DO itrc_loc = 1, ntracers_loc
                  trc_sm_carry_loc(itrc_loc, ipatch) = 0._r8
               ENDDO
            ENDIF
            END BLOCK

            ! Update saved states to post-THERMAL for WATER delta tracking
            wliq_soisno_old_trc(lb:nl_soil) = wliq_soisno(lb:nl_soil)
            wice_soisno_old_trc(lb:nl_soil) = wice_soisno(lb:nl_soil)
#endif

         IF (.not. DEF_USE_VariablySaturatedFlow) THEN

            CALL WATER_2014 (ipatch,patchtype         ,lb                ,nl_soil           ,&
                 deltim            ,z_soisno(lb:)     ,dz_soisno(lb:)    ,zi_soisno(lb-1:)  ,&
                 bsw               ,porsl             ,psi0              ,hksati            ,&
                 theta_r           ,fsatmax           ,fsatdcf           ,elvstd            ,&
                 BVIC              ,rootr             ,rootflux          ,t_soisno(lb:)     ,&
                 wliq_soisno(lb:)  ,wice_soisno(lb:)  ,smp               ,hk                ,&
                 pg_rain           ,sm                ,etr               ,qseva             ,&
                 qsdew             ,qsubl             ,qfros             ,qseva_soil        ,&
                 qsdew_soil        ,qsubl_soil        ,qfros_soil        ,qseva_snow        ,&
                 qsdew_snow        ,qsubl_snow        ,qfros_snow        ,fsno              ,&
                 rsur              ,rnof              ,qinfl             ,pondmx            ,&
                 ssi               ,wimp              ,smpmin            ,zwt               ,&
                 wdsrf             ,wa                ,qcharge           ,&

#if (defined CaMa_Flood)
                 !add variables for flood depth [mm], flood fraction [0-1]
                 !and re-infiltration [mm/s] calculation.
                 flddepth          ,fldfrc            ,qinfl_fld         ,&
#endif
! SNICAR model variables
                 forc_aer          ,&
                 mss_bcpho(lbsn:0) ,mss_bcphi(lbsn:0) ,mss_ocpho(lbsn:0) ,mss_ocphi(lbsn:0) ,&
                 mss_dst1(lbsn:0)  ,mss_dst2(lbsn:0)  ,mss_dst3(lbsn:0)  ,mss_dst4(lbsn:0)  ,&
!  irrigation variables
                 qflx_irrig_drip   ,qflx_irrig_flood  ,qflx_irrig_paddy)
                 rsub = rnof - rsur
         ELSE

            CALL WATER_VSF (ipatch ,patchtype,is_dry_lake,   lb          ,nl_soil           ,&
                 deltim            ,z_soisno(lb:)     ,dz_soisno(lb:)    ,zi_soisno(lb-1:)  ,&
                 bsw               ,theta_r           ,fsatmax           ,fsatdcf           ,&
                 topoweti          ,alp_twi           ,chi_twi           ,mu_twi            ,&
                 elvstd            ,BVIC              ,&
#ifdef vanGenuchten_Mualem_SOIL_MODEL
                 alpha_vgm         ,n_vgm             ,L_vgm             ,sc_vgm            ,&
                 fc_vgm            ,&
#endif
                 porsl             ,psi0              ,hksati            ,rootr             ,&
                 rootflux          ,t_soisno(lb:)     ,wliq_soisno(lb:)  ,wice_soisno(lb:)  ,&
                 smp               ,hk                ,pg_rain           ,sm                ,&
                 etr               ,qseva             ,qsdew             ,qsubl             ,&
                 qfros             ,qseva_soil        ,qsdew_soil        ,qsubl_soil        ,&
                 qfros_soil        ,qseva_snow        ,qsdew_snow        ,qsubl_snow        ,&
                 qfros_snow        ,fsno              ,frcsat            ,rsur              ,&
                 rsur_se           ,rsur_ie           ,rsub              ,rnof              ,&
                 qinfl                                                                      ,&
                 qlayer            ,ssi               ,pondmx            ,wimp              ,&
                 zwt               ,wdsrf             ,wa                ,wetwat            ,&
                 etroot_trc                                                                 ,&
                 wblc_ice_sink_trc                                                          ,&
                 etroot_actual_trc                                                          ,&
                 etroot_aquifer_trc                                                         ,&
#if (defined CaMa_Flood)
                 !add variables for flood depth [mm], flood fraction [0-1]
                 !and re-infiltration [mm/s] calculation.
                 flddepth          ,fldfrc            ,qinfl_fld         ,&
#endif
! SNICAR model variables
                 forc_aer          ,&
                 mss_bcpho(lbsn:0) ,mss_bcphi(lbsn:0) ,mss_ocpho(lbsn:0) ,mss_ocphi(lbsn:0) ,&
                 mss_dst1(lbsn:0)  ,mss_dst2(lbsn:0)  ,mss_dst3(lbsn:0)  ,mss_dst4(lbsn:0)  ,&
!  irrigation variables
                 qflx_irrig_drip   ,qflx_irrig_flood  ,qflx_irrig_paddy)
         ENDIF

#ifdef TRACER
            ! WATER_VSF takes the wetland-merge branch (L1170+ in
            ! MOD_SoilSnowHydrology) when patchtype==2 .and. not
            ! DEF_USE_Dynamic_Wetland. In that branch wdsrf/wa/wresi are
            ! absorbed into wetwat, so the standard surface+soil tracer
            ! path does not apply. tracer_wetland mirrors the water
            ! merge, routing pool tracers proportionally.
            IF (patchtype == 2 .and. .not. DEF_USE_Dynamic_Wetland) THEN
               CALL tracer_wetland(ipatch, deltim, snl, nl_soil, &
                  rsur, &
                  qseva, qsdew, qsubl, qfros, &
                  qseva_soil, qsdew_soil, qsubl_soil, qfros_soil, &
                  qseva_snow, qsdew_snow, qsubl_snow, qfros_snow, &
                  etr, sm, fsno, DEF_SPLIT_SOILSNOW, &
                  wliq_soisno(snl+1:nl_soil), wice_soisno(snl+1:nl_soil), &
                  wliq_soisno_old_trc, wice_soisno_old_trc, &
                  wa, wa_old_trc, wdsrf, wdsrf_old_trc, &
                  wetwat, wetwat_old_trc, pg_rain, pg_snow, &
                  t_soisno(snl+1:nl_soil), porsl(1:nl_soil), &
                  dz_soisno(snl+1:nl_soil), &
                  qflx_irrig_drip + qflx_irrig_flood + qflx_irrig_paddy, &
                  waterstorage_trc_ground, &
                  forc_q_frac = forc_q, &
                  forc_psrf_frac = forc_psrf)
            ELSE
               CALL tracer_soil_water(ipatch, deltim, snl, nl_soil, &
                  qlayer, qinfl, qcharge, rsur, rsub, &
                  qseva, qsdew, qsubl, qfros, &
                  qseva_soil, qsdew_soil, qsubl_soil, qfros_soil, &
                  qseva_snow, qsdew_snow, qsubl_snow, qfros_snow, &
                  sm, fsno, DEF_SPLIT_SOILSNOW, &
                  wliq_soisno(snl+1:nl_soil), wice_soisno(snl+1:nl_soil), &
                  wliq_soisno_old_trc, wice_soisno_old_trc, &
                  wa, wa_old_trc, wdsrf, wdsrf_old_trc, &
	                  wetwat, wetwat_old_trc, pg_rain, pg_snow, &
	                  etroot_trc, wblc_ice_sink_trc, &
		                  etroot_actual_trc, etroot_aquifer_trc, &
		                  qflx_irrig_drip + qflx_irrig_flood + qflx_irrig_paddy, &
			                  waterstorage_trc_ground, &
			                  tleaf_frac = tleaf, &
		                  t_soisno_frac = t_soisno(snl+1:nl_soil), &
		                  forc_q_frac = forc_q, &
		                  forc_psrf_frac = forc_psrf, &
		                  lai_frac = lai, &
	                  rst_frac = rst)
	            ENDIF

            ! tracer_runoff removed: surface and subsurface runoff
            ! are now fully handled inside tracer_soil_water / tracer_wetland
#endif

         IF (snl < 0) THEN
            ! Compaction rate for snow
            ! Natural compaction and metamorphosis. The compaction rate
            ! is recalculated for every new timestep
            lb  = snl + 1   !lower bound of array
            CALL snowcompaction (lb,deltim,&
                            imelt(lb:0),fiold(lb:0),t_soisno(lb:0),&
                            wliq_soisno(lb:0),wice_soisno(lb:0),forc_us,forc_vs,dz_soisno(lb:0))

            ! Combine thin snow elements
            lb = maxsnl + 1

            ! Tracer arrays follow the snow-column topology exactly via the
            ! optional trc_wliq / trc_wice / trc_scv hooks in combine/divide
            ! (see MOD_SnowLayersCombineDivide.F90). The prior approach of
            ! post-hoc redistribution via tracer_snow_layer_adj homogenised
            ! the column and erased isotope gradients — it has been removed.
            IF (DEF_USE_SNICAR) THEN
#ifdef TRACER
                  CALL snowlayerscombine_snicar (lb,snl,&
                               z_soisno(lb:1),dz_soisno(lb:1),zi_soisno(lb-1:1),&
                               wliq_soisno(lb:1),wice_soisno(lb:1),t_soisno(lb:1),scv,snowdp,&
                               mss_bcpho(lb:0), mss_bcphi(lb:0), mss_ocpho(lb:0), mss_ocphi(lb:0),&
                               mss_dst1(lb:0), mss_dst2(lb:0), mss_dst3(lb:0), mss_dst4(lb:0), &
                               trc_wliq = trc_wliq_soisno(:, lb:1, ipatch), &
                               trc_wice = trc_wice_soisno(:, lb:1, ipatch), &
                               trc_scv  = trc_scv(:, ipatch))
#else
                  CALL snowlayerscombine_snicar (lb,snl,&
                               z_soisno(lb:1),dz_soisno(lb:1),zi_soisno(lb-1:1),&
                               wliq_soisno(lb:1),wice_soisno(lb:1),t_soisno(lb:1),scv,snowdp,&
                               mss_bcpho(lb:0), mss_bcphi(lb:0), mss_ocpho(lb:0), mss_ocphi(lb:0),&
                               mss_dst1(lb:0), mss_dst2(lb:0), mss_dst3(lb:0), mss_dst4(lb:0) )
#endif
            ELSE
#ifdef TRACER
                  CALL snowlayerscombine (lb,snl,&
                               z_soisno(lb:1),dz_soisno(lb:1),zi_soisno(lb-1:1),&
                               wliq_soisno(lb:1),wice_soisno(lb:1),t_soisno(lb:1),scv,snowdp, &
                               trc_wliq = trc_wliq_soisno(:, lb:1, ipatch), &
                               trc_wice = trc_wice_soisno(:, lb:1, ipatch), &
                               trc_scv  = trc_scv(:, ipatch))
#else
                  CALL snowlayerscombine (lb,snl,&
                               z_soisno(lb:1),dz_soisno(lb:1),zi_soisno(lb-1:1),&
                               wliq_soisno(lb:1),wice_soisno(lb:1),t_soisno(lb:1),scv,snowdp)
#endif
            ENDIF

            ! Divide thick snow elements
            IF(snl<0) THEN
               IF (DEF_USE_SNICAR) THEN
#ifdef TRACER
                     CALL snowlayersdivide_snicar (lb,snl,&
                               z_soisno(lb:0),dz_soisno(lb:0),zi_soisno(lb-1:0),&
                               wliq_soisno(lb:0),wice_soisno(lb:0),t_soisno(lb:0),&
                               mss_bcpho(lb:0),mss_bcphi(lb:0),mss_ocpho(lb:0),mss_ocphi(lb:0),&
                               mss_dst1(lb:0),mss_dst2(lb:0),mss_dst3(lb:0),mss_dst4(lb:0), &
                               trc_wliq = trc_wliq_soisno(:, lb:0, ipatch), &
                               trc_wice = trc_wice_soisno(:, lb:0, ipatch))
#else
                     CALL snowlayersdivide_snicar (lb,snl,&
                               z_soisno(lb:0),dz_soisno(lb:0),zi_soisno(lb-1:0),&
                               wliq_soisno(lb:0),wice_soisno(lb:0),t_soisno(lb:0),&
                               mss_bcpho(lb:0),mss_bcphi(lb:0),mss_ocpho(lb:0),mss_ocphi(lb:0),&
                               mss_dst1(lb:0),mss_dst2(lb:0),mss_dst3(lb:0),mss_dst4(lb:0) )
#endif
               ELSE
#ifdef TRACER
                     CALL snowlayersdivide (lb,snl,&
                               z_soisno(lb:0),dz_soisno(lb:0),zi_soisno(lb-1:0),&
                               wliq_soisno(lb:0),wice_soisno(lb:0),t_soisno(lb:0), &
                               trc_wliq = trc_wliq_soisno(:, lb:0, ipatch), &
                               trc_wice = trc_wice_soisno(:, lb:0, ipatch))
#else
                     CALL snowlayersdivide (lb,snl,&
                               z_soisno(lb:0),dz_soisno(lb:0),zi_soisno(lb-1:0),&
                               wliq_soisno(lb:0),wice_soisno(lb:0),t_soisno(lb:0))
#endif
               ENDIF
            ENDIF
         ENDIF

         ! Set zero to the empty node
         IF (snl > maxsnl) THEN
            wice_soisno(maxsnl+1:snl) = 0.
            wliq_soisno(maxsnl+1:snl) = 0.
            t_soisno   (maxsnl+1:snl) = 0.
            z_soisno   (maxsnl+1:snl) = 0.
            dz_soisno  (maxsnl+1:snl) = 0.
         ENDIF

         lb = snl + 1
         t_grnd = t_soisno(lb)

         IF (is_dry_lake) THEN
            dz_lake = wdsrf*1.e-3/nl_lake
            t_lake  = t_soisno(1)
            IF (t_soisno(1) >= tfrz) THEN
               lake_icefrac = 0.
            ELSE
               lake_icefrac = 1.
            ENDIF

            IF (wdsrf >= 100.) THEN
               CALL adjust_lake_layer (nl_lake, dz_lake, t_lake, lake_icefrac)
            ENDIF
         ENDIF

         ! ----------------------------------------
         ! energy balance
         ! ----------------------------------------
         zerr=errore
#if (defined CoLMDEBUG)
         IF (abs(errore) > .5) THEN
            write(6,*) 'Warning: energy balance violation ',errore,patchclass
         ENDIF
#endif

         ! ----------------------------------------
         ! water balance
         ! ----------------------------------------
         endwb=sum(wice_soisno(1:)+wliq_soisno(1:))+ldew+scv + wa
#ifdef CROP
         IF (DEF_USE_IRRIGATION) endwb = endwb + waterstorage(ipatch)
#endif

         endwb = endwb + wdsrf
         IF (DEF_USE_VariablySaturatedFlow) THEN
            IF (patchtype == 2) THEN
               endwb = endwb + wetwat
            ENDIF
         ENDIF
#if (defined CaMa_Flood)
         IF (LWINFILT) THEN
            IF (patchtype == 0) THEN
               endwb=endwb - qinfl_fld*deltim
            ENDIF
         ENDIF
#endif

#ifndef CatchLateralFlow
         errorw=(endwb-totwb)-(forc_prc+forc_prl-fevpa-rnof)*deltim
#else
         ! for lateral flow, "rsur" is considered in HYDRO/MOD_Hydro_SurfaceFlow.F90
         errorw=(endwb-totwb)-(forc_prc+forc_prl-fevpa)*deltim
#endif

         IF (.not. DEF_USE_VariablySaturatedFlow) THEN
            IF (patchtype==2) errorw=0.    !wetland
         ENDIF

         xerr=errorw/deltim

#ifdef TRACER
            CALL tracer_apply_reactive_processes(ipatch, snl, nl_soil, deltim)
#ifndef CatchLateralFlow
            CALL tracer_balance_check(ipatch, snl, nl_soil, deltim, xerr_tracer, &
               patchtype_in = patchtype, water_err_in = errorw, &
               water_dS_in = endwb - totwb, &
               water_input_in = (forc_prc + forc_prl) * deltim, &
               water_output_in = (fevpa + rnof) * deltim, &
               water_evap_in = fevpa * deltim, &
               water_rnof_in = rnof * deltim)
#else
            CALL tracer_balance_check(ipatch, snl, nl_soil, deltim, xerr_tracer, &
               patchtype_in = patchtype, water_err_in = errorw, &
               water_dS_in = endwb - totwb, &
               water_input_in = (forc_prc + forc_prl) * deltim, &
               water_output_in = fevpa * deltim, &
               water_evap_in = fevpa * deltim, &
               water_rnof_in = 0._r8)
#endif

            CALL tracer_hist_accumulate(ipatch, snl, maxsnl, nl_soil, ldew_rain, ldew_snow, &
               wliq_soisno(snl+1:nl_soil), wice_soisno(snl+1:nl_soil), &
               wa, wdsrf, wetwat, scv)
            deallocate(wliq_soisno_old_trc, wice_soisno_old_trc)
            deallocate(wice_snow_bef_trc)
#endif

         ! Phase-change mass arrays are allocated unconditionally before
         ! THERMAL; mirror that here so the deallocate fires for both
         ! tracer-on and tracer-off runs.
         IF (allocated(soil_thaw_mass_th)) deallocate(soil_thaw_mass_th)
         IF (allocated(soil_frzc_mass_th)) deallocate(soil_frzc_mass_th)

#if (defined LULC_IGBP_PFT || defined LULC_IGBP_PC)
         IF (allocated(canopy_phase_heat_p)) deallocate(canopy_phase_heat_p)
#endif

#if (defined CoLMDEBUG)
         IF (abs(errorw) > 1.e-3) THEN
            IF     (patchtype == 0) THEN
               write(6,*) 'Warning: water balance violation in CoLMMAIN (soil) ', errorw
            ELSEIF (patchtype == 1) THEN
               write(6,*) 'Warning: water balance violation in CoLMMAIN (urban) ', errorw
            ELSEIF (patchtype == 2) THEN
               write(6,*) 'Warning: water balance violation in CoLMMAIN (wetland) ', errorw
            ELSEIF (patchtype == 4) THEN
               write(6,*) 'Warning: water balance violation in CoLMMAIN (dry lake) ', errorw
            ENDIF
            CALL CoLM_stop ()
         ENDIF
#endif

!======================================================================

      ELSEIF (patchtype == 3) THEN   ! <=== is LAND ICE (glacier/ice sheet) (patchtype = 3)

!======================================================================
                            ! initial set
         scvold = scv       ! snow mass at previous time step
         glacier_overflow_mass_trc = 0._r8

         snl = 0
         DO j=maxsnl+1,0
            IF(wliq_soisno(j)+wice_soisno(j)>0.) snl=snl-1
         ENDDO

         zi_soisno(0)=0.
         IF (snl < 0) THEN
            DO j = -1, snl, -1
               zi_soisno(j)=zi_soisno(j+1)-dz_soisno(j+1)
            ENDDO
         ENDIF
         DO j = 1,nl_soil
            zi_soisno(j)=zi_soisno(j-1)+dz_soisno(j)
         ENDDO

         totwb = scv + sum(wice_soisno(1:)+wliq_soisno(1:))
         IF (DEF_USE_VariablySaturatedFlow) THEN
            totwb = wdsrf + totwb
         ENDIF

         fiold(:) = 0.0
         IF (snl <0 ) THEN
            fiold(snl+1:0)=wice_soisno(snl+1:0)/(wliq_soisno(snl+1:0)+wice_soisno(snl+1:0))
         ENDIF

         pg_rain = prc_rain + prl_rain
         pg_snow = prc_snow + prl_snow

         t_rain = t_precip
         IF (wliq_soisno(1) > dz_soisno(1)*denh2o) THEN
            wextra  = (wliq_soisno(1) - dz_soisno(1)*denh2o) / deltim
            t_rain  = (pg_rain*t_precip + wextra*t_soisno(1)) / (pg_rain + wextra)
            pg_rain = pg_rain + wextra
            wliq_soisno(1) = dz_soisno(1)*denh2o
            totwb = totwb - wextra*deltim
            glacier_overflow_mass_trc = glacier_overflow_mass_trc + wextra*deltim
         ENDIF

         t_snow = t_precip
         IF (wice_soisno(1) > dz_soisno(1)*denice) THEN
            wextra  = (wice_soisno(1) - dz_soisno(1)*denice) / deltim
            t_snow  = (pg_snow*t_precip + wextra*t_soisno(1)) / (pg_snow + wextra)
            pg_snow = pg_snow + wextra
            wice_soisno(1) = dz_soisno(1)*denice
            totwb = totwb - wextra*deltim
            glacier_overflow_mass_trc = glacier_overflow_mass_trc + wextra*deltim
         ENDIF

         IF (pg_rain+pg_snow > 0) THEN
            t_precip = (pg_rain*cpliq*t_rain + pg_snow*cpice*t_snow)/(pg_rain*cpliq+pg_snow*cpice)
         ENDIF

         !----------------------------------------------------------------
         ! Initialize new snow nodes for snowfall / sleet
         !----------------------------------------------------------------

         snl_bef = snl

         CALL newsnow (patchtype,maxsnl,deltim,t_grnd,pg_rain,pg_snow,bifall,&
                       t_precip,zi_soisno(:0),z_soisno(:0),dz_soisno(:0),t_soisno(:0),&
                       wliq_soisno(:0),wice_soisno(:0),fiold(:0),snl,sag,scv,snowdp,fsno)

         !----------------------------------------------------------------
         ! Energy and Water balance
         !----------------------------------------------------------------
         lb   = snl + 1            !lower bound of array
         lbsn = min(lb,0)

         CALL GLACIER_TEMP (patchtype,lb       ,nl_soil    ,deltim      ,&
                      zlnd        ,zsno        ,capr       ,cnfac       ,&
                      forc_hgt_u  ,forc_hgt_t  ,forc_hgt_q ,forc_us     ,&
                      forc_vs     ,forc_t      ,forc_q     ,forc_hpbl   ,&
                      forc_rhoair ,forc_psrf   ,coszen     ,sabg        ,&
                      forc_frl    ,fsno        ,dz_soisno(lb:)          ,&
                      z_soisno(lb:),zi_soisno(lb-1:)       ,&
                      t_soisno(lb:),wice_soisno(lb:),wliq_soisno(lb:)   ,&
                      scv         ,snowdp      ,imelt(lb:) ,taux        ,&
                      tauy        ,fsena       ,fevpa      ,lfevpa      ,&
                      fseng       ,fevpg       ,olrg       ,fgrnd       ,&
                      qseva       ,qsdew       ,qsubl      ,qfros       ,&
                      sm          ,tref        ,qref       ,trad        ,&
                      errore      ,emis        ,z0m        ,zol         ,&
                      rib         ,ustar       ,qstar      ,tstar       ,&
                      fm          ,fh          ,fq         ,pg_rain     ,&
                      pg_snow     ,t_precip    ,&
                      snofrz(lbsn:0), sabg_snow_lyr(lb:1)                )


         IF (DEF_USE_SNICAR) THEN
            CALL GLACIER_WATER_snicar (nl_soil ,maxsnl     ,deltim      ,&
                      z_soisno    ,dz_soisno   ,zi_soisno  ,t_soisno    ,&
                      wliq_soisno ,wice_soisno ,pg_rain    ,pg_snow     ,&
                      sm          ,scv         ,snowdp     ,imelt       ,&
                      fiold       ,snl         ,qseva      ,qsdew       ,&
                      qsubl       ,qfros       ,gwat       ,ssi         ,&
                      wimp        ,forc_us     ,forc_vs    ,&
                      ! SNICAR
                      forc_aer    ,&
                      mss_bcpho   ,mss_bcphi   ,mss_ocpho  ,mss_ocphi   ,&
                      mss_dst1    ,mss_dst2    ,mss_dst3   ,mss_dst4     )
         ELSE
            CALL GLACIER_WATER (   nl_soil     ,maxsnl     ,deltim      ,&
                      z_soisno    ,dz_soisno   ,zi_soisno  ,t_soisno    ,&
                      wliq_soisno ,wice_soisno ,pg_rain    ,pg_snow     ,&
                      sm          ,scv         ,snowdp     ,imelt       ,&
                      fiold       ,snl         ,qseva      ,qsdew       ,&
                      qsubl       ,qfros       ,gwat       ,ssi         ,&
                      wimp        ,forc_us     ,forc_vs                  )
         ENDIF

         IF (.not. DEF_USE_VariablySaturatedFlow) THEN
            rsur = max(0.0,gwat)
            rsub = 0.
            rnof = rsur
         ELSE
            a = wdsrf + wliq_soisno(1) + gwat * deltim
            IF (a > dz_soisno(1)*denh2o) THEN
               wliq_soisno(1) = dz_soisno(1)*denh2o
               wdsrf = a - wliq_soisno(1)
            ELSE
               wdsrf = 0.
               wliq_soisno(1) = max(a, 1.e-8)
            ENDIF
#ifndef CatchLateralFlow
            IF (wdsrf > pondmx) THEN
               rsur  = (wdsrf - pondmx) / deltim
               wdsrf = pondmx
            ELSE
               rsur = 0.
            ENDIF
            rsub = 0.
            rnof = rsur
            rsur_se = rsur
            rsur_ie = 0.
#endif
         ENDIF

         lb = snl + 1
         t_grnd = t_soisno(lb)

         ! ----------------------------------------
         ! energy and water balance check
         ! ----------------------------------------
         zerr=errore

         endwb = scv + sum(wice_soisno(1:)+wliq_soisno(1:))
         IF (DEF_USE_VariablySaturatedFlow) THEN
            endwb = wdsrf + endwb
         ENDIF

#ifndef CatchLateralFlow
         errorw=(endwb-totwb)-(pg_rain+pg_snow-fevpa-rnof)*deltim
#else
         errorw=(endwb-totwb)-(pg_rain+pg_snow-fevpa)*deltim
#endif

#if (defined CoLMDEBUG)
         IF (DEF_USE_VariablySaturatedFlow) THEN
            IF (abs(errorw) > 1.e-3) THEN
               write(6,*) 'Warning: water balance violation in CoLMMAIN (land ice) ', errorw
               CALL CoLM_stop ()
            ENDIF
         ENDIF
#endif

         IF (DEF_USE_VariablySaturatedFlow) THEN
            xerr=errorw/deltim
         ELSE
            xerr = 0.
         ENDIF

         ! Glacier (patchtype=3) tracer handling.
         ! Glacier branch does not run tracer_precip/_soil_water, so the
         ! prognostic pools (trc_wliq_soisno, trc_wice_soisno, trc_scv,
         ! trc_wa, trc_wdsrf) would stay at their initial values while
         ! the water pools evolve through GLACIER_WATER — producing
         ! trc/water ratios that drift arbitrarily (source of the
         ! ±1000‰ δ artefacts). Rebuild the pools with the ratio implied
         ! by the step tracer budget: fixed-signature tracers keep R_init,
         ! while runtime-forced / fractionating tracers use a mixed-box
         ! update with phase-resolved deposition and evaporation. Then
         ! record precip input, evap/dew, and runoff so the global
         ! accumulators stay consistent with the water-side balance. Finally
         ! call tracer_hist_accumulate so the
         ! per-pool δ diagnostics (MOD_Hist f_trc_conc_*) get a fresh
         ! entry for this patch; without it glacier pixels emit spval
         ! (no data) at history time. Runtime-forced / fractionating
         ! tracers use a mixed-box update so this branch no longer
         ! erases their signature back to R_init every step.
#ifdef TRACER
            BLOCK
            USE MOD_Tracer_Defs, only: ntracers, tracer_init_water_ratio, trc_tiny, &
                                       tracer_can_use_fixed_signature
            USE MOD_Tracer_Forcing, only: tracer_forcing_precip_value, &
                                          tracer_forcing_vapor_value
            USE MOD_Tracer_Frac, only: tracer_fractionation_active, tracer_surface_relhum, &
                                       tracer_diffusivity_ratio_air, &
                                       tracer_craig_gordon_evap_ratio, &
                                       tracer_equilibrium_deposition_ratio
            USE MOD_Tracer_Vars, only: trc_rnof_step_g => trc_rnof_step, &
                                       a_trc_precip_g => a_trc_precip, &
                                       a_trc_evap_g   => a_trc_evap, &
                                       a_trc_rsur_g   => a_trc_rsur, &
                                       a_trc_rnof_g   => a_trc_rnof, &
                                       trc_ldew_rain, trc_ldew_snow, &
                                       trc_wetwat, trc_waterstorage, &
                                       trc_storage_beg, trc_runtime_forced, &
                                       sync_tracer_patch_ratio
            USE MOD_Tracer_Conservation, only: tracer_save_storage, tracer_balance_check, &
               tracer_apply_reactive_processes
            USE MOD_Tracer_Hist, only: tracer_hist_accumulate
            integer  :: itrc_g, j_trc_g, snl_trc_g
            real(r8) :: R_init_g, R_precip_g, R_vapor_g, R_out_g, R_final_g
            real(r8) :: R_dew_g, R_frost_g, R_evap_liq_g, R_evap_ice_g, R_runoff_g
            real(r8) :: precip_mass_g, rnof_mass_g
            real(r8) :: evap_mass_g, dep_mass_g
            real(r8) :: evap_liq_mass_g, evap_ice_mass_g, dep_liq_mass_g, dep_ice_mass_g
            real(r8) :: water_dS_g, water_end_g, water_beg_g, water_input_g
            real(r8) :: water_before_output_g, water_after_evap_g
            real(r8) :: trc_input_g, trc_evap_g, trc_rnof_g
            real(r8) :: trc_available_g, trc_final_g
            real(r8) :: relhum_liq_g, relhum_ice_g, alpha_k_g
            logical  :: mixed_signature_g, fixed_signature_g, frac_active_g
            ! Purge pools that don't belong to a glacier patch
            ! (canopy, wetland, irrigation reservoir) BEFORE the
            ! storage snapshot. If an earlier LULCC transition left
            ! foreign mass in these slots, including it in
            ! storage_beg would produce a phantom conservation
            ! spike when the mixed-box rebuild subsequently zeroes
            ! them. Clearing here makes storage_beg and storage_end
            ! agree on the zero baseline.
            DO itrc_g = 1, ntracers
               trc_ldew_rain(itrc_g, ipatch) = 0._r8
               trc_ldew_snow(itrc_g, ipatch) = 0._r8
               trc_wetwat   (itrc_g, ipatch) = 0._r8
               IF (allocated(trc_waterstorage)) THEN
                  trc_waterstorage(itrc_g, ipatch) = 0._r8
               ENDIF
            ENDDO

            ! Glacier water balance above uses the aggregate snow storage
            ! `scv` plus land-ice layers 1:nl_soil. Negative-index snow
            ! layers are internal topology and are not part of endwb/totwb.
            ! Keep tracer storage on the same accounting basis; otherwise
            ! layer reshuffling shows up as a false tracer residual.
            snl_trc_g = 0
            CALL tracer_save_storage(ipatch, snl_trc_g, nl_soil)
            ! The glacier water code temporarily moves layer-1 overflow
            ! (`wextra`) out of the beginning storage and into pg_rain /
            ! pg_snow; those two edits cancel in the water balance. The
            ! tracer storage snapshot is the real pre-step storage, so
            ! tracer external input must use only atmospheric precip.
            precip_mass_g = (prc_rain + prl_rain + prc_snow + prl_snow) * deltim
            rnof_mass_g   = max(rnof, 0._r8) * deltim
            ! Use gross phase-resolved fluxes instead of net fevpa so active
            ! fractionation sees evaporation/sublimation and dew/frost as
            ! separate processes when they coexist within one step.
            evap_liq_mass_g = max(qseva, 0._r8) * deltim
            evap_ice_mass_g = max(qsubl, 0._r8) * deltim
            dep_liq_mass_g  = max(qsdew, 0._r8) * deltim
            dep_ice_mass_g  = max(qfros, 0._r8) * deltim
            evap_mass_g = evap_liq_mass_g + evap_ice_mass_g
            dep_mass_g  = dep_liq_mass_g  + dep_ice_mass_g
            water_input_g = precip_mass_g + dep_mass_g
            water_dS_g = endwb - totwb - glacier_overflow_mass_trc
            water_end_g = max(wdsrf, 0._r8) + max(scv, 0._r8)
            DO j_trc_g = 1, nl_soil
               water_end_g = water_end_g + max(wliq_soisno(j_trc_g), 0._r8) &
                  + max(wice_soisno(j_trc_g), 0._r8)
            ENDDO
            water_beg_g = water_end_g - water_dS_g
            DO itrc_g = 1, ntracers
               trc_rnof_step_g(itrc_g, ipatch) = 0._r8
               R_init_g = tracer_init_water_ratio(itrc_g)
               frac_active_g = tracer_fractionation_active(itrc_g)
               fixed_signature_g = tracer_can_use_fixed_signature(itrc_g) .and. .not. frac_active_g
               IF (allocated(trc_runtime_forced)) THEN
                  fixed_signature_g = fixed_signature_g .and. .not. trc_runtime_forced(itrc_g)
               ENDIF
               mixed_signature_g = .not. fixed_signature_g

               IF (mixed_signature_g) THEN
                  R_precip_g = tracer_forcing_precip_value(itrc_g, ipatch)
                  R_vapor_g  = tracer_forcing_vapor_value (itrc_g, ipatch)
                  R_dew_g = R_vapor_g
                  R_frost_g = R_vapor_g
                  IF (frac_active_g) THEN
                     R_dew_g = tracer_equilibrium_deposition_ratio(itrc_g, R_vapor_g, t_grnd, .false.)
                     R_frost_g = tracer_equilibrium_deposition_ratio(itrc_g, R_vapor_g, t_grnd, .true.)
                  ENDIF
                  trc_input_g = precip_mass_g * R_precip_g + dep_liq_mass_g * R_dew_g &
                     + dep_ice_mass_g * R_frost_g
                  trc_available_g = max(trc_storage_beg(itrc_g, ipatch) + trc_input_g, 0._r8)
                  water_before_output_g = water_beg_g + water_input_g
                  IF (water_before_output_g > trc_tiny) THEN
                     R_out_g = trc_available_g / water_before_output_g
                  ELSE
                     R_out_g = R_init_g
                  ENDIF
                  R_evap_liq_g = R_out_g
                  R_evap_ice_g = R_out_g
                  IF (frac_active_g) THEN
                     alpha_k_g = tracer_diffusivity_ratio_air(itrc_g)
                     relhum_liq_g = tracer_surface_relhum(forc_q, forc_psrf, t_grnd, .false.)
                     relhum_ice_g = tracer_surface_relhum(forc_q, forc_psrf, t_grnd, .true.)
                     R_evap_liq_g = tracer_craig_gordon_evap_ratio(itrc_g, R_out_g, R_vapor_g, &
                        t_grnd, relhum_liq_g, alpha_k_g, .false.)
                     R_evap_ice_g = tracer_craig_gordon_evap_ratio(itrc_g, R_out_g, R_vapor_g, &
                        t_grnd, relhum_ice_g, alpha_k_g, .true.)
                     R_evap_liq_g = min(R_evap_liq_g, max(R_out_g, 0._r8))
                     R_evap_ice_g = min(R_evap_ice_g, max(R_out_g, 0._r8))
                  ENDIF
                  trc_evap_g = min(evap_liq_mass_g * R_evap_liq_g + &
                     evap_ice_mass_g * R_evap_ice_g, trc_available_g)
                  water_after_evap_g = water_before_output_g - evap_mass_g
                  IF (water_after_evap_g > trc_tiny) THEN
                     R_runoff_g = max(trc_available_g - trc_evap_g, 0._r8) / water_after_evap_g
                  ELSE
                     R_runoff_g = R_out_g
                  ENDIF
                  trc_rnof_g = min(rnof_mass_g * R_runoff_g, &
                     max(trc_available_g - trc_evap_g, 0._r8))
                  trc_final_g = max(trc_available_g - trc_evap_g - trc_rnof_g, 0._r8)
                  IF (water_end_g > trc_tiny) THEN
                     R_final_g = trc_final_g / water_end_g
                  ELSE
                     R_final_g = 0._r8
                  ENDIF
               ELSE
                  trc_input_g = water_input_g * R_init_g
                  trc_evap_g  = evap_mass_g * R_init_g
                  trc_rnof_g  = rnof_mass_g * R_init_g
                  R_final_g   = R_init_g
               ENDIF

               IF (trc_input_g > 0._r8) THEN
                  a_trc_precip_g(itrc_g, ipatch) = a_trc_precip_g(itrc_g, ipatch) &
                     + trc_input_g
               ENDIF
               IF (trc_evap_g > 0._r8) THEN
                  a_trc_evap_g(itrc_g, ipatch) = a_trc_evap_g(itrc_g, ipatch) + trc_evap_g
               ENDIF
               IF (trc_rnof_g > 0._r8) THEN
                  trc_rnof_step_g(itrc_g, ipatch) = trc_rnof_g
                  a_trc_rsur_g(itrc_g, ipatch) = a_trc_rsur_g(itrc_g, ipatch) + trc_rnof_g
                  a_trc_rnof_g(itrc_g, ipatch) = a_trc_rnof_g(itrc_g, ipatch) + trc_rnof_g
               ENDIF
               CALL sync_tracer_patch_ratio(itrc_g, ipatch, snl_trc_g, maxsnl, nl_soil, &
                  wliq_soisno, wice_soisno, 0._r8, wdsrf, scv, R_final_g)
            ENDDO
            ! Close the per-step balance now that storage_end and
            ! accumulator increments (precip/evap/rnof) reflect this
            ! step fully. Use the same glacier accounting bound as
            ! save_storage.
            CALL tracer_apply_reactive_processes(ipatch, snl_trc_g, nl_soil, deltim)
            CALL tracer_balance_check(ipatch, snl_trc_g, nl_soil, deltim, xerr_tracer, &
               patchtype_in = patchtype, water_err_in = errorw, &
               water_dS_in = water_dS_g, &
               water_input_in = precip_mass_g + dep_mass_g, &
               water_output_in = evap_mass_g + rnof_mass_g, &
               water_evap_in = evap_mass_g, water_rnof_in = rnof_mass_g)
            ! Feed the per-pool δ diagnostic. Glacier has no canopy
            ! (ldew_rain/snow=0 by rebuild) and no wetland pool; pass
            ! their current values so tracer_hist_accumulate sums a
            ! zero mass/water pair and the history writer emits spval
            ! (rather than zero) for those pools.
            CALL tracer_hist_accumulate(ipatch, snl_trc_g, maxsnl, nl_soil, &
               0._r8, 0._r8, &
               wliq_soisno(snl_trc_g+1:nl_soil), wice_soisno(snl_trc_g+1:nl_soil), &
               0._r8, wdsrf, 0._r8, scv)
            END BLOCK
#endif

	!======================================================================

      ELSEIF (patchtype == 4) THEN   ! <=== is LAND WATER BODIES
                                     ! (lake, reservoir and river) (patchtype = 4)

!======================================================================

         totwb = scv + sum(wice_soisno(1:)+wliq_soisno(1:)) + wa
         IF (DEF_USE_Dynamic_Lake) THEN
            totwb = totwb + wdsrf
         ENDIF

         snl = 0
         DO j = maxsnl+1, 0
            IF (wliq_soisno(j)+wice_soisno(j) > 0.) THEN
               snl=snl-1
            ENDIF
         ENDDO

         zi_soisno(0) = 0.
         IF (snl < 0) THEN
            DO j = -1, snl, -1
               zi_soisno(j)=zi_soisno(j+1)-dz_soisno(j+1)
            ENDDO
         ENDIF

         DO j = 1,nl_soil
            zi_soisno(j)=zi_soisno(j-1)+dz_soisno(j)
         ENDDO

         scvold = scv          !snow mass at previous time step
         fiold(:) = 0.0
         IF (snl < 0) THEN
            fiold(snl+1:0)=wice_soisno(snl+1:0)/(wliq_soisno(snl+1:0)+wice_soisno(snl+1:0))
         ENDIF

         w_old = sum(wliq_soisno(1:)) + sum(wice_soisno(1:))

         pg_rain = prc_rain + prl_rain
         pg_snow = prc_snow + prl_snow

#ifndef EXTERNAL_LAKE
         CALL newsnow_lake ( DEF_USE_Dynamic_Lake, &
              ! "in" arguments
              ! ---------------
              maxsnl       ,nl_lake      ,deltim          ,dz_lake         ,&
              pg_rain      ,pg_snow      ,t_precip        ,bifall          ,&

              ! "inout" arguments
              ! ------------------
              t_lake       ,zi_soisno(:0),z_soisno(:0)    ,&
              dz_soisno(:0),t_soisno(:0) ,wliq_soisno(:0) ,wice_soisno(:0) ,&
              fiold(:0)    ,snl          ,sag             ,scv             ,&
              snowdp       ,lake_icefrac )

         CALL laketem ( &
              ! "in" laketem arguments
              ! ---------------------------
              patchtype    ,maxsnl       ,nl_soil         ,nl_lake         ,&
              patchlatr    ,deltim       ,forc_hgt_u      ,forc_hgt_t      ,&
              forc_hgt_q   ,forc_us      ,forc_vs         ,forc_t          ,&
              forc_q       ,forc_rhoair  ,forc_psrf       ,forc_sols       ,&
              forc_soll    ,forc_solsd   ,forc_solld      ,sabg            ,&
              forc_frl     ,dz_soisno    ,z_soisno        ,zi_soisno       ,&
              dz_lake      ,lakedepth    ,vf_quartz       ,vf_gravels      ,&
              vf_om        ,vf_sand      ,wf_gravels      ,wf_sand         ,&
              porsl        ,csol         ,k_solids        ,&
              dksatu       ,dksatf       ,dkdry           ,&
              BA_alpha     ,BA_beta      ,forc_hpbl       ,&

              ! "inout" laketem arguments
              ! ---------------------------
              t_grnd       ,scv          ,snowdp          ,t_soisno        ,&
              wliq_soisno  ,wice_soisno  ,imelt           ,t_lake          ,&
              lake_icefrac ,savedtke1    ,&

! SNICAR model variables
              snofrz       ,sabg_snow_lyr,&
! END SNICAR model variables

              ! "out" laketem arguments
              ! ---------------------------
              taux         ,tauy         ,fsena           ,&
              fevpa        ,lfevpa       ,fseng           ,fevpg           ,&
              qseva        ,qsubl        ,qsdew           ,qfros           ,&
              olrg         ,fgrnd        ,tref            ,qref            ,&
              trad         ,emis         ,z0m             ,zol             ,&
              rib          ,ustar        ,qstar           ,tstar           ,&
              fm           ,fh           ,fq              ,sm               )

         CALL snowwater_lake ( DEF_USE_Dynamic_Lake, &
              ! "in" snowater_lake arguments
              ! ---------------------------
              maxsnl       ,nl_soil      ,nl_lake         ,deltim          ,&
              ssi          ,wimp         ,porsl           ,pg_rain         ,&
              pg_snow      ,dz_lake      ,imelt(:0)       ,fiold(:0)       ,&
              qseva        ,qsubl        ,qsdew           ,qfros           ,&

              ! "inout" snowater_lake arguments
              ! ---------------------------
              z_soisno     ,dz_soisno    ,zi_soisno       ,t_soisno        ,&
              wice_soisno  ,wliq_soisno  ,t_lake          ,lake_icefrac    ,&
              gwat         ,&
              fseng        ,fgrnd        ,snl             ,scv             ,&
              snowdp       ,sm           ,forc_us         ,forc_vs         ,&

              ! SNICAR model variables
              forc_aer     ,&
              mss_bcpho    ,mss_bcphi    ,mss_ocpho       ,mss_ocphi       ,&
              mss_dst1     ,mss_dst2     ,mss_dst3        ,mss_dst4         )

#else
         CALL external_lake( &
               ! "in" arguments
               ! -------------------
               deltim      ,patchlatr     ,patchlonr      ,bifall        ,&
               forc_hgt_u  ,forc_hgt_t    ,forc_hgt_q     ,forc_us       ,&
               forc_vs     ,forc_t        ,forc_q         ,forc_rhoair   ,&
               forc_psrf   ,forc_frl      ,sabg           ,forc_hpbl     ,&
               forc_sols   ,forc_soll     ,forc_solsd     ,forc_solld    ,&
               prc_rain    ,prl_rain      ,prc_snow       ,prl_snow      ,&
               t_precip    ,ipatch        ,&
               ! "inout" arguments
               ! -------------------
               t_grnd      ,t_lake        ,t_soisno       ,snl           ,&
               z_soisno    ,zi_soisno     ,dz_soisno      ,scv           ,&
               savedtke1   ,sag           ,snowdp         ,lake_icefrac  ,&
               wliq_soisno ,wice_soisno   ,gwat           ,&
! SNICAR model variables
               forc_aer    ,sabg_snow_lyr ,snofrz         ,&
               mss_bcpho   ,mss_bcphi     ,mss_ocpho      ,mss_ocphi     ,&
               mss_dst1    ,mss_dst2      ,mss_dst3       ,mss_dst4      ,&
! END SNICAR model variables
               ! "out" arguments
               ! -------------------
               fsena       ,fevpa         ,lfevpa         ,fseng         ,&
               fevpg       ,olrg          ,fgrnd          ,trad          ,&
               qseva       ,qsubl         ,qsdew          ,qfros         ,&
               taux        ,tauy          ,ustar          ,qstar         ,&
               tstar       ,emis          ,sm             ,zol           ,&
               tref        ,qref          ,fm             ,fq            ,&
               rib         ,fh            ,z0m            )
#endif

         IF (.not. DEF_USE_Dynamic_Lake) THEN
            ! We assume the land water bodies have zero extra liquid water capacity
            ! (i.e.,constant capacity), all excess liquid water are put into the runoff,
            ! this unreasonable assumption should be updated in the future version
            a = (sum(wliq_soisno(1:))+sum(wice_soisno(1:))+scv-w_old-scvold)/deltim
            aa = qseva+qsubl-qsdew-qfros
            rsur = max(0., pg_rain + pg_snow - aa - a)
            rsub = 0.
            rnof = rsur
            rsur_se = rsur
            rsur_ie = 0.
            lake_deficit = - min(0., pg_rain + pg_snow - aa - a)
         ELSE

            wdsrf = sum(dz_lake) * 1.e3

#ifndef CatchLateralFlow
            IF (wdsrf > lakedepth*1.e3) THEN
               rsur  = (wdsrf - lakedepth*1.e3) / deltim
               wdsrf = lakedepth*1.e3
               dz_lake = dz_lake * lakedepth/sum(dz_lake)
               CALL adjust_lake_layer (nl_lake, dz_lake, t_lake, lake_icefrac)
            ELSE
               rsur = 0.
            ENDIF
            rsub = 0.
            rnof = rsur
            rsur_se = rsur
            rsur_ie = 0.
#endif
         ENDIF

         endwb  = scv + sum(wice_soisno(1:)+wliq_soisno(1:)) + wa
         IF (DEF_USE_Dynamic_Lake) THEN
            endwb  = endwb  + wdsrf
         ELSE
            endwb  = endwb  - lake_deficit * deltim
         ENDIF

         errorw = (endwb-totwb) - (forc_prc+forc_prl-fevpa) * deltim
#ifndef CatchLateralFlow
         errorw = errorw + rnof * deltim
#endif

#if (defined CoLMDEBUG)
         IF (abs(errorw) > 1.e-3) THEN
            write(*,*) 'Warning: water balance violation in CoLMMAIN (lake) ', errorw
            CALL CoLM_stop ()
         ENDIF
#endif

         IF (DEF_USE_Dynamic_Lake) THEN
            xerr = errorw / deltim
         ELSE
            xerr = 0.
         ENDIF

         ! Set zero to the empty node
         IF (snl > maxsnl) THEN
            wice_soisno(maxsnl+1:snl) = 0.
            wliq_soisno(maxsnl+1:snl) = 0.
            t_soisno   (maxsnl+1:snl) = 0.
            z_soisno   (maxsnl+1:snl) = 0.
            dz_soisno  (maxsnl+1:snl) = 0.
         ENDIF

         ! Waterbody (patchtype=4) tracer handling. See glacier block
         ! for rationale. The mixed-box update covers lake/reservoir evolution
         ! via laketem/snowwater_lake/external_lake — without it the
         ! lake's `wa`, `wdsrf` tracer would never update even as the
         ! water pool changes, yielding meaningless δ values. The
         ! follow-up evap/dew and hist_accumulate calls mirror the
         ! glacier branch so lake pixels contribute to the per-pool
         ! history diagnostics instead of emitting spval.
#ifdef TRACER
            BLOCK
            USE MOD_Tracer_Defs, only: ntracers, tracer_init_water_ratio, trc_tiny, &
                                       tracer_can_use_fixed_signature
            USE MOD_Tracer_Forcing, only: tracer_forcing_precip_value, &
                                          tracer_forcing_vapor_value
            USE MOD_Tracer_Frac, only: tracer_fractionation_active, tracer_surface_relhum, &
                                       tracer_diffusivity_ratio_air, &
                                       tracer_craig_gordon_evap_ratio, &
                                       tracer_equilibrium_deposition_ratio
            USE MOD_Tracer_Vars, only: trc_rnof_step_w => trc_rnof_step, &
                                       a_trc_precip_w => a_trc_precip, &
                                       a_trc_evap_w   => a_trc_evap, &
                                       a_trc_rsur_w   => a_trc_rsur, &
                                       a_trc_rnof_w   => a_trc_rnof, &
                                       trc_ldew_rain, trc_ldew_snow, &
                                       trc_wetwat, trc_waterstorage, &
                                       trc_storage_beg, trc_runtime_forced, &
                                       sync_tracer_patch_ratio
            USE MOD_Tracer_Conservation, only: tracer_save_storage, tracer_balance_check, &
               tracer_apply_reactive_processes
            USE MOD_Tracer_Hist, only: tracer_hist_accumulate
            integer  :: itrc_w, j_trc_w
            real(r8) :: R_init_w, R_precip_w, R_vapor_w, R_pool_w, R_out_w, R_final_w
            real(r8) :: R_dew_w, R_frost_w, R_evap_liq_w, R_evap_ice_w, R_runoff_w
            real(r8) :: atm_precip_mass_w, deficit_mass_w, precip_mass_w, rnof_mass_w
            real(r8) :: evap_mass_w, dep_mass_w
            real(r8) :: evap_liq_mass_w, evap_ice_mass_w, dep_liq_mass_w, dep_ice_mass_w
            real(r8) :: water_dS_w, water_end_w, water_beg_w, water_input_w
            real(r8) :: water_before_output_w, water_after_evap_w
            real(r8) :: trc_input_w, trc_evap_w, trc_rnof_w
            real(r8) :: trc_available_w, trc_final_w
            real(r8) :: relhum_liq_w, relhum_ice_w, alpha_k_w
            logical  :: mixed_signature_w, fixed_signature_w, frac_active_w
            ! Purge foreign pools before the storage snapshot, so a
            ! LULCC class switch from soil/crop to waterbody does
            ! not carry canopy / wetland / irrigation tracer mass
            ! into the lake's inventory. See glacier block for
            ! reasoning.
            DO itrc_w = 1, ntracers
               trc_ldew_rain(itrc_w, ipatch) = 0._r8
               trc_ldew_snow(itrc_w, ipatch) = 0._r8
               trc_wetwat   (itrc_w, ipatch) = 0._r8
               IF (allocated(trc_waterstorage)) THEN
                  trc_waterstorage(itrc_w, ipatch) = 0._r8
               ENDIF
            ENDDO

            CALL tracer_save_storage(ipatch, maxsnl, nl_soil)
            ! Non-Dynamic_Lake closes its water budget by treating
            ! `lake_deficit` as a phantom precip input (L1815, L1840).
            ! Mirror that accounting in tracer. For runtime-forced tracers,
            ! this numerical fill uses the old pool ratio so it does not
            ! impose an atmospheric isotope signature.
            atm_precip_mass_w = (forc_rain + forc_snow) * deltim
            IF (.not. DEF_USE_Dynamic_Lake) THEN
               deficit_mass_w = lake_deficit * deltim
            ELSE
               deficit_mass_w = 0._r8
            ENDIF
            precip_mass_w = atm_precip_mass_w + deficit_mass_w
            rnof_mass_w   = max(rnof, 0._r8) * deltim
            evap_liq_mass_w = max(qseva, 0._r8) * deltim
            evap_ice_mass_w = max(qsubl, 0._r8) * deltim
            dep_liq_mass_w  = max(qsdew, 0._r8) * deltim
            dep_ice_mass_w  = max(qfros, 0._r8) * deltim
            evap_mass_w = evap_liq_mass_w + evap_ice_mass_w
            dep_mass_w  = dep_liq_mass_w  + dep_ice_mass_w
            water_input_w = precip_mass_w + dep_mass_w
            water_dS_w = endwb - totwb
            water_end_w = wa + max(wdsrf, 0._r8)
            DO j_trc_w = maxsnl + 1, nl_soil
               IF (j_trc_w >= snl + 1) THEN
                  water_end_w = water_end_w + max(wliq_soisno(j_trc_w), 0._r8) &
                     + max(wice_soisno(j_trc_w), 0._r8)
               ENDIF
            ENDDO
            IF (snl >= 0) water_end_w = water_end_w + max(scv, 0._r8)
            water_beg_w = water_end_w - water_dS_w
            DO itrc_w = 1, ntracers
               trc_rnof_step_w(itrc_w, ipatch) = 0._r8
               R_init_w = tracer_init_water_ratio(itrc_w)
               frac_active_w = tracer_fractionation_active(itrc_w)
               fixed_signature_w = tracer_can_use_fixed_signature(itrc_w) .and. .not. frac_active_w
               IF (allocated(trc_runtime_forced)) THEN
                  fixed_signature_w = fixed_signature_w .and. .not. trc_runtime_forced(itrc_w)
               ENDIF
               mixed_signature_w = .not. fixed_signature_w

               IF (mixed_signature_w) THEN
                  R_precip_w = tracer_forcing_precip_value(itrc_w, ipatch)
                  R_vapor_w  = tracer_forcing_vapor_value (itrc_w, ipatch)
                  IF (water_beg_w > trc_tiny) THEN
                     R_pool_w = max(trc_storage_beg(itrc_w, ipatch), 0._r8) / water_beg_w
                  ELSE
                     R_pool_w = R_precip_w
                  ENDIF
                  R_dew_w = R_vapor_w
                  R_frost_w = R_vapor_w
                  IF (frac_active_w) THEN
                     R_dew_w = tracer_equilibrium_deposition_ratio(itrc_w, R_vapor_w, t_grnd, .false.)
                     R_frost_w = tracer_equilibrium_deposition_ratio(itrc_w, R_vapor_w, t_grnd, .true.)
                  ENDIF
                  trc_input_w = atm_precip_mass_w * R_precip_w &
                     + dep_liq_mass_w * R_dew_w + dep_ice_mass_w * R_frost_w &
                     + deficit_mass_w * R_pool_w
                  trc_available_w = max(trc_storage_beg(itrc_w, ipatch) + trc_input_w, 0._r8)
                  water_before_output_w = water_beg_w + water_input_w
                  IF (water_before_output_w > trc_tiny) THEN
                     R_out_w = trc_available_w / water_before_output_w
                  ELSE
                     R_out_w = R_init_w
                  ENDIF
                  R_evap_liq_w = R_out_w
                  R_evap_ice_w = R_out_w
                  IF (frac_active_w) THEN
                     alpha_k_w = tracer_diffusivity_ratio_air(itrc_w)
                     relhum_liq_w = tracer_surface_relhum(forc_q, forc_psrf, t_grnd, .false.)
                     relhum_ice_w = tracer_surface_relhum(forc_q, forc_psrf, t_grnd, .true.)
                     R_evap_liq_w = tracer_craig_gordon_evap_ratio(itrc_w, R_out_w, R_vapor_w, &
                        t_grnd, relhum_liq_w, alpha_k_w, .false.)
                     R_evap_ice_w = tracer_craig_gordon_evap_ratio(itrc_w, R_out_w, R_vapor_w, &
                        t_grnd, relhum_ice_w, alpha_k_w, .true.)
                     R_evap_liq_w = min(R_evap_liq_w, max(R_out_w, 0._r8))
                     R_evap_ice_w = min(R_evap_ice_w, max(R_out_w, 0._r8))
                  ENDIF
                  trc_evap_w = min(evap_liq_mass_w * R_evap_liq_w + &
                     evap_ice_mass_w * R_evap_ice_w, trc_available_w)
                  water_after_evap_w = water_before_output_w - evap_mass_w
                  IF (water_after_evap_w > trc_tiny) THEN
                     R_runoff_w = max(trc_available_w - trc_evap_w, 0._r8) / water_after_evap_w
                  ELSE
                     R_runoff_w = R_out_w
                  ENDIF
                  trc_rnof_w = min(rnof_mass_w * R_runoff_w, &
                     max(trc_available_w - trc_evap_w, 0._r8))
                  trc_final_w = max(trc_available_w - trc_evap_w - trc_rnof_w, 0._r8)
                  IF (water_end_w > trc_tiny) THEN
                     R_final_w = trc_final_w / water_end_w
                  ELSE
                     R_final_w = 0._r8
                  ENDIF
               ELSE
                  trc_input_w = water_input_w * R_init_w
                  trc_evap_w  = evap_mass_w * R_init_w
                  trc_rnof_w  = rnof_mass_w * R_init_w
                  R_final_w   = R_init_w
               ENDIF

               IF (trc_input_w > 0._r8) THEN
                  a_trc_precip_w(itrc_w, ipatch) = a_trc_precip_w(itrc_w, ipatch) &
                     + trc_input_w
               ENDIF
               IF (trc_evap_w > 0._r8) THEN
                  a_trc_evap_w(itrc_w, ipatch) = a_trc_evap_w(itrc_w, ipatch) + trc_evap_w
               ENDIF
               IF (trc_rnof_w > 0._r8) THEN
                  trc_rnof_step_w(itrc_w, ipatch) = trc_rnof_w
                  a_trc_rsur_w(itrc_w, ipatch) = a_trc_rsur_w(itrc_w, ipatch) + trc_rnof_w
                  a_trc_rnof_w(itrc_w, ipatch) = a_trc_rnof_w(itrc_w, ipatch) + trc_rnof_w
               ENDIF
               CALL sync_tracer_patch_ratio(itrc_w, ipatch, snl, maxsnl, nl_soil, &
                  wliq_soisno, wice_soisno, wa, wdsrf, scv, R_final_w)
            ENDDO
            ! Mirror save_storage iteration bound (see the note there)
            ! so storage_beg and storage_end cover the same range.
            CALL tracer_apply_reactive_processes(ipatch, maxsnl, nl_soil, deltim)
            CALL tracer_balance_check(ipatch, maxsnl, nl_soil, deltim, xerr_tracer, &
               patchtype_in = patchtype, water_err_in = errorw, &
               water_dS_in = water_dS_w, &
               water_input_in = precip_mass_w + dep_mass_w, &
               water_output_in = evap_mass_w + rnof_mass_w, &
               water_evap_in = evap_mass_w, water_rnof_in = rnof_mass_w)
            ! wa is meaningful for the waterbody branch (dynamic lake
            ! can debit it), so feed it to the history accumulator.
            ! No canopy / wetland — pass 0 for those.
            CALL tracer_hist_accumulate(ipatch, snl, maxsnl, nl_soil, &
               0._r8, 0._r8, &
               wliq_soisno(snl+1:nl_soil), wice_soisno(snl+1:nl_soil), &
               wa, wdsrf, 0._r8, scv)
            END BLOCK
#endif

!======================================================================

      ELSE                     ! <=== is OCEAN (patchtype >= 99)

!======================================================================
! simple ocean-sea ice model

         tssea = t_grnd
         tssub (1:7) = t_soisno (1:7)
         CALL SOCEAN (dosst,deltim,oro,forc_hgt_u,forc_hgt_t,forc_hgt_q,&
                    forc_us,forc_vs,forc_t,forc_t,forc_rhoair,forc_psrf,&
                    sabg,forc_frl,tssea,tssub(1:7),scv,&
                    taux,tauy,fsena,fevpa,lfevpa,fseng,fevpg,tref,qref,&
                    z0m,zol,rib,ustar,qstar,tstar,fm,fh,fq,emis,olrg)

                  ! null data for sea component
                    z_soisno   (:) = 0.0
                    dz_soisno  (maxsnl+1:0) = 0.
                    t_soisno   (:) = 0.0
                    t_soisno (1:7) = tssub(1:7)
                    wliq_soisno(:) = 0.0
                    wice_soisno(:) = 0.0
                    t_grnd  = tssea
                    snowdp  = scv/1000.*20.

                    trad    = tssea
                    fgrnd   = 0.0
                    rsur    = 0.0
                    rsur_se = 0.0
                    rsur_ie = 0.0
                    rsub    = 0.0
                    rnof    = 0.0
                    xerr    = 0.0

!======================================================================
      ENDIF

#if (defined CaMa_Flood)
      IF (LWEVAP) THEN
         IF ((flddepth .gt. 1.e-6).and.(fldfrc .gt. 0.05).and.patchtype == 0)THEN
            CALL get_fldevp (forc_hgt_u,forc_hgt_t,forc_hgt_q,&
               forc_us,forc_vs,forc_t,forc_q,forc_rhoair,forc_psrf,t_grnd,&
               forc_hpbl, &
               taux_fld,tauy_fld,fseng_fld,fevpg_fld,tref_fld,qref_fld,&
               z0m_fld,zol_fld,rib_fld,ustar_fld,qstar_fld,tstar_fld,fm_fld,fh_fld,fq_fld)

            IF (fevpg_fld<0.0) fevpg_fld=0.0d0

            IF ((flddepth-deltim*fevpg_fld .gt. 0.0) .and. (fevpg_fld.gt.0.0)) THEN
               flddepth=flddepth-deltim*fevpg_fld
               fseng= fseng_fld*fldfrc+(1.0-fldfrc)*fseng
               fevpg= fevpg_fld*fldfrc+(1.0-fldfrc)*fevpg
               fevpg_fld=fevpg_fld*fldfrc
            ELSE
               fevpg_fld=0.0d0
            ENDIF

         ELSE
            fevpg_fld=0.0d0
         ENDIF

      ELSE
         fevpg_fld=0.0d0
      ENDIF
#endif


!======================================================================
! Preparation for the next time step
! 1) time-varying parameters for vegetation
! 2) fraction of snow cover
! 3) solar zenith angle and
! 4) albedos
!======================================================================

      ! cosine of solar zenith angle
      calday = calendarday(idate)
      coszen = orb_coszen(calday,patchlonr,patchlatr)

      IF (patchtype <= 5) THEN   !LAND
#if (defined DYN_PHENOLOGY)
         ! need to update lai and sai, fveg, green, they are done once in a day only
         IF (dolai) THEN
            CALL LAI_empirical(patchclass,nl_soil,rootfr,t_soisno(1:),lai,sai,fveg,green)
         ENDIF
#endif

! only for soil patches
!NOTE: lai from remote sensing has already considered snow coverage

!NOTE: IF account for snow on vegetation:
!        1) should use snow-free LAI data and 2) update LAI and SAI according to snowdp

         IF (patchtype == 0) THEN

#if (defined LULC_USGS || defined LULC_IGBP)
            CALL snowfraction (tlai(ipatch),tsai(ipatch),z0m,zlnd,scv,snowdp,wt,sigf,fsno)
            lai = tlai(ipatch)
            sai = tsai(ipatch) * sigf

            !NOTE: use snow-free LAI by defining namelist DEF_VEG_SNOW
            IF ( DEF_VEG_SNOW ) THEN
               lai = tlai(ipatch) * sigf
            ENDIF
#endif

#if (defined LULC_IGBP_PFT || defined LULC_IGBP_PC)
            ps = patch_pft_s(ipatch)
            pe = patch_pft_e(ipatch)
            CALL snowfraction_pftwrap (ipatch,zlnd,scv,snowdp,wt,sigf,fsno)
            IF(DEF_USE_LAIFEEDBACK)THEN
               lai = sum(lai_p(ps:pe)*pftfrac(ps:pe))
            ELSE
               lai_p(ps:pe) = tlai_p(ps:pe)
               lai = tlai(ipatch)

               !NOTE: use snow-free LAI by defining namelist DEF_VEG_SNOW
               IF ( DEF_VEG_SNOW ) THEN
                  lai_p(ps:pe) = tlai_p(ps:pe)*sigf_p(ps:pe)
                  lai = sum(lai_p(ps:pe)*pftfrac(ps:pe))
               ENDIF
            ENDIF
            sai_p(ps:pe) = tsai_p(ps:pe) * sigf_p(ps:pe)
            sai = sum(sai_p(ps:pe)*pftfrac(ps:pe))
#endif

         ELSE
            CALL snowfraction (tlai(ipatch),tsai(ipatch),z0m,zlnd,scv,snowdp,wt,sigf,fsno)
            lai = tlai(ipatch)
            sai = tsai(ipatch) * sigf

            !NOTE: use snow-free LAI by defining namelist DEF_VEG_SNOW
            IF ( DEF_VEG_SNOW ) THEN
               lai = tlai(ipatch) * sigf
            ENDIF
         ENDIF

         ! water volumetric content of soil surface layer [m3/m3]
         ssw = min(1.,1.e-3*wliq_soisno(1)/dz_soisno(1))
         IF (patchtype >= 3) ssw = 1.0

! ============================================================================
! Snow aging routine based on Flanner and Zender (2006), Linking snowpack
! microphysics and albedo evolution, JGR, and Brun (1989), Investigation of
! wet-snow metamorphism in respect of liquid-water content, Ann. Glacial.

         dz_soisno_(:1) = dz_soisno(:1)
         t_soisno_ (:1) = t_soisno (:1)

         IF ((patchtype == 4) .and. (.not. is_dry_lake)) THEN
            dz_soisno_(1) = dz_lake(1)
            t_soisno_ (1) = t_lake (1)
         ENDIF

! ============================================================================
         ! albedos
         ! we supposed CALL it every time-step, because
         ! other vegetation related parameters are needed to create
         IF (doalb) THEN
#ifdef HYPERSPECTRAL
            CALL albland_HiRes (ipatch, patchtype,deltim,&
                 soil_s_v_alb,soil_d_v_alb,soil_s_n_alb,soil_d_n_alb,&
                 chil,rho,tau,fveg,green,lai,sai,fwet_snow,coszen,&
                 wt,fsno,scv,scvold,sag,ssw,pg_snow,forc_t,t_grnd,t_soisno_,dz_soisno_,&
                 snl,wliq_soisno,wice_soisno,snw_rds,snofrz,&
                 mss_bcpho,mss_bcphi,mss_ocpho,mss_ocphi,&
                 mss_dst1,mss_dst2,mss_dst3,mss_dst4,&
                 alb,ssun,ssha,ssoi,ssno,ssno_lyr,thermk,extkb,extkd,&

                 ! new parameters for hyperspectral scheme
                 alb_hires                         ,&
                 dir_frac    , dif_frac            ,&
                 reflectance , transmittance       ,&
                 soil_alb, kw, nw, porsl(1)        ,&
                 reflectance_out, transmittance_out,&
                 idate(2), patchlatr, patchlonr    ,&
                 urban_albedo, mean_albedo, lat_north, lat_south, lon_west, lon_east )

#else
            CALL albland (ipatch,patchtype,deltim,&
                 soil_s_v_alb,soil_d_v_alb,soil_s_n_alb,soil_d_n_alb,&
                 chil,rho,tau,fveg,green,lai,sai,fwet_snow,coszen,&
                 wt,fsno,scv,scvold,sag,ssw,pg_snow,forc_t,t_grnd,t_soisno_,dz_soisno_,&
                 snl,wliq_soisno,wice_soisno,snw_rds,snofrz,&
                 mss_bcpho,mss_bcphi,mss_ocpho,mss_ocphi,&
                 mss_dst1,mss_dst2,mss_dst3,mss_dst4,&
                 alb,ssun,ssha,ssoi,ssno,ssno_lyr,thermk,extkb,extkd)
#endif
         ENDIF

      ELSE                   !OCEAN
         sag = 0.0
         IF(doalb)THEN
            CALL albocean (oro,scv,coszen,alb)
         ENDIF
      ENDIF

      ! zero-filling set for glacier/ice-sheet/land water bodies/ocean components
      IF ((patchtype > 2) .and. (.not. is_dry_lake)) THEN
         lai           = 0.0
         sai           = 0.0
         laisun        = 0.0
         laisha        = 0.0
         green         = 0.0
         fveg          = 0.0
         sigf          = 0.0

         ssun(:,:)     = 0.0
         ssha(:,:)     = 0.0
         thermk        = 0.0
         extkb         = 0.0
         extkd         = 0.0

         tleaf         = forc_t
         ldew_rain     = 0.0
         ldew_snow     = 0.0
         fwet_snow     = 0.0
         ldew          = 0.0
         fsenl         = 0.0
         fevpl         = 0.0
         etr           = 0.0
         assim         = 0.0
         respc         = 0.0

         zerr          = 0.

         qinfl         = 0.
         qlayer        = 0.
         qdrip         = forc_rain + forc_snow
         qintr         = 0.
         frcsat        = 1.
         h2osoi        = 0.
         rstfacsun_out = 0.
         rstfacsha_out = 0.
         gssun_out     = 0.
         gssha_out     = 0.
         assimsun_out  = 0.
         etrsun_out    = 0.
         assimsha_out  = 0.
         etrsha_out    = 0.
         rootr         = 0.
         rootflux      = 0.
         zwt           = 0.

         IF (.not. DEF_USE_VariablySaturatedFlow) THEN
            wa = 4800.
         ENDIF

         qcharge = 0.
         IF (DEF_USE_PLANTHYDRAULICS)THEN
            vegwp = -2.5e4
         ENDIF
      ENDIF

      h2osoi = wliq_soisno(1:)/(dz_soisno(1:)*denh2o) + wice_soisno(1:)/(dz_soisno(1:)*denice)

      IF (DEF_USE_VariablySaturatedFlow) THEN
         wat = sum(wice_soisno(1:)+wliq_soisno(1:))+ldew+scv+wetwat
      ELSE
         wat = sum(wice_soisno(1:)+wliq_soisno(1:))+ldew+scv + wa
      ENDIF

      z_sno (maxsnl+1:0) = z_soisno (maxsnl+1:0)
      dz_sno(maxsnl+1:0) = dz_soisno(maxsnl+1:0)

END SUBROUTINE CoLMMAIN
! ---------- EOP ------------
